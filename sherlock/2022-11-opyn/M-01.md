# Potential DoS in `depositsQueued` and `withdrawsQueued` functions

## Summary

There is a potential Denial of Service in the functions `depositsQueued` and `withdrawsQueued`. Each of these functions traverse an array to calculate it's result and given a large amount of elements may lead to a DoS due to high gas costs involved in the calculations.

## Vulnerability Detail

The function `depositsQueued` and `withdrawsQueued` present in the `CrabNetting` contract are used to account for how much deposits or withdrawals are still queued in the contract, that is, the sum of each element's amount starting from the last processed index. 

The `depositsQueued` function:

https://github.com/sherlock-audit/2022-11-opyn-romeroadrian/blob/main/crab-netting/src/CrabNetting.sol#L424

```solidity
function depositsQueued() external view returns (uint256) {
    uint256 j = depositsIndex;
    uint256 sum;
    while (j < deposits.length) {
        sum = sum + deposits[j].amount;
        j++;
    }
    return sum;
}
```

And similarly, the `withdrawsQueued` function:

https://github.com/sherlock-audit/2022-11-opyn-romeroadrian/blob/main/crab-netting/src/CrabNetting.sol#L437

```solidity
function withdrawsQueued() external view returns (uint256) {
    uint256 j = withdrawsIndex;
    uint256 sum;
    while (j < withdraws.length) {
        sum = sum + withdraws[j].amount;
        j++;
    }
    return sum;
}
```

In both cases, the calculation involves traversing the full array (or the unprocessed part of it) and reading each position from storage. Even deleted or zeroed elements are accounted in the for loop.

## Impact

Given a large amount of deposits or withdrawals, this may lead to an eventual denial of service due to high gas costs. Note that this applies to both on-chain and off-chain events. 

Other parts of the Opyn protocol or any other protocol that wants to integrate with the `CrabNetting` contract may need to use any of these functions to query for the queued deposits or withdrawals amounts. 

Also, node providers that expose JSON-RPC API may have a cap on gas limit in the `eth_call` or `eth_estimateGas` endpoints to prevent against this kind of attacks.

## Test and Gas Report

The following tests simulate 1000 queued deposits and withdraws, and then a call to `depositsQueued` and `withdrawsQueued` to estimate the gas cost of each function.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {BaseForkSetup} from "./BaseForkSetup.t.sol";

contract AuditTest is BaseForkSetup {
    function test_CrabNetting_depositsQueued_DoS() public {
        vm.prank(0x57757E3D981446D585Af0D9Ae4d7DF6D64647806);
        usdc.transfer(depositor, 1000);

        vm.prank(depositor);
        usdc.approve(address(netting), type(uint256).max);

        for (uint256 i = 0; i < 1000; i++) {
            vm.prank(depositor);
            netting.depositUSDC(1);
        }

        netting.depositsQueued();
    }

    function test_CrabNetting_withdrawsQueued_DoS() public {
        vm.prank(0x06CECFbac34101aE41C88EbC2450f8602b3d164b);
        crab.transfer(depositor, 1000);

        vm.prank(depositor);
        crab.approve(address(netting), type(uint256).max);

        for (uint256 i = 0; i < 1000; i++) {
            vm.prank(depositor);
            netting.queueCrabForWithdrawal(1);
        }

        netting.withdrawsQueued();
    }
}
```

The gas report for the `depositsQueued` function shows more than half a million units of gas:

```solidity
| src/CrabNetting.sol:CrabNetting contract |                 |        |        |        |         |
|------------------------------------------|-----------------|--------|--------|--------|---------|
| Deployment Cost                          | Deployment Size |        |        |        |         |
| 4734858                                  | 25560           |        |        |        |         |
| Function Name                            | min             | avg    | median | max    | # calls |
| DOMAIN_SEPARATOR                         | 365             | 365    | 365    | 365    | 1       |
| depositUSDC                              | 61708           | 77189  | 77135  | 146835 | 1000    |
| depositsQueued                           | 596518          | 596518 | 596518 | 596518 | 1       |
```

The gas report for the `withdrawsQueued` function shows a similarly high value:

```solidity
| src/CrabNetting.sol:CrabNetting contract |                 |        |        |        |         |
|------------------------------------------|-----------------|--------|--------|--------|---------|
| Deployment Cost                          | Deployment Size |        |        |        |         |
| 4734858                                  | 25560           |        |        |        |         |
| Function Name                            | min             | avg    | median | max    | # calls |
| DOMAIN_SEPARATOR                         | 365             | 365    | 365    | 365    | 1       |
| queueCrabForWithdrawal                   | 61862           | 77381  | 77327  | 147027 | 1000    |
| withdrawsQueued                          | 596584          | 596584 | 596584 | 596584 | 1       |
```

## Tool used

Manual Review

## Recommendation

Instead of traversing the array of deposits or withdrawals, have an accumulator to store the sum of these amounts that is updated whenever the array is modified.  

For example, for the deposits side, this accumulator variable would be incremented in the `depositUSDC` function and decrement in `withdrawUSDC`, `netAtPrice` and `depositAuction`.

Reading from this accumulator would then just involve a single sload.
