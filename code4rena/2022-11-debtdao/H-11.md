# Closing a paid credit may lead to incorrectly stepping the queue

Closing a credit will call the `_repay` internal function, which will update the credit's balances and step the queue if the credit's principal has been already paid. 

This will behave incorrectly if the queue was already "stepped" when the credit was repaid, as it will generate a second call to step the queue when the credit gets closed.

## Impact

If a credit gets repaid in full (principal goes to 0) using the `depositAndRepay` function, the internal `_repay` function will correctly call the `stepQ` function to move this credit out of the queue and position the next one with debt at the front of queue, according to the FIFO schedule.

Now, when this credit gets closed using the `close` function, the `_repay` function will be called again, and because principal is still 0, this will generate a new call to `stepQ` which will incorrectly shift the queue again, potentially moving the next credit with debt from the front of the queue.

This will break the invariant that the queue is ordered by debt, since it may lead to a credit without principal at the first position, while having credits with principal debt deeper in the queue, as shown in the following PoC.

## PoC

The test will create 3 different credits from 3 lenders. Borrower will first borrow from the first two credits. The queue then is:

```
[1, 2, 3]
```

Now borrower repays principal + interest accrued for the first credit, this will step the queue and move the first element out of the queue into the last position:

```
[2, 3, 1]
```

Everything goes fine at this point, credit 2 still has unpaid principal, while 3 was never borrowed and 1 has been repaid. Now credit 1 gets closed, this will step the queue *again* as principal is 0:

```
[3, 1 (null deleted), 2]
```

We end up with credit 3 at the front of the queue which isn't borrowed, while credit 2 (still unpaid) is last.


Note: the context for this test (setup, variables and helper functions) is similar to the one found in the file `LineOfCredit.t.sol`.

```
function test_close_IncorrectlyStepQueue() public {
    address lender1 = makeAddr("lender1");
    _mintAndApprove(lender1);
    address lender2 = makeAddr("lender2");
    _mintAndApprove(lender2);
    address lender3 = makeAddr("lender3");
    _mintAndApprove(lender3);
    
    address token = address(supportedToken1);

    _addCredit(lender1, token, 1 ether);
    _addCredit(lender2, token, 1 ether);
    _addCredit(lender3, token, 1 ether);

    bytes32 id1 = CreditLib.computeId(address(line), lender1, token);
    bytes32 id2 = CreditLib.computeId(address(line), lender2, token);
    bytes32 id3 = CreditLib.computeId(address(line), lender3, token);
    
    // Borrow from credit 1 and credit 2
    vm.prank(borrower);
    line.borrow(id1, 1);
    vm.prank(borrower);
    line.borrow(id2, 1);
    
    // Queue should be 1 - 2 - 3
    assertEq(line.ids(0), id1);
    assertEq(line.ids(1), id2);
    assertEq(line.ids(2), id3);
    
    vm.warp(block.timestamp + 30 days);
    line.updateOutstandingDebt();
    
    // Repay credit 1
    (, uint256 principal1, uint256 interestAccrued1,,,,)  = line.credits(id1);
    uint256 debt = principal1 + interestAccrued1;
    vm.prank(borrower);
    line.depositAndRepay(debt);
    
    // Queue should be 2 - 3 - 1, this is correct so far: credit 2 has debt and is in position 0
    assertEq(line.ids(0), id2);
    assertEq(line.ids(1), id3);
    assertEq(line.ids(2), id1);
    
    vm.warp(block.timestamp + 1 days);
    
    // Now here is the bug, we call close on credit1, but this will incorrectly step the queue, removing credit 2 from the top
    vm.prank(lender1);
    line.close(id1);
    
    // Queue is now be 3 - 1 (deleted) - 2
    assertEq(line.ids(0), id3);
    assertEq(line.ids(1), bytes32(0));
    assertEq(line.ids(2), id2);
    
    // We have broken the invariant here: credit 3 is next in the queue but its principal is 0. Credit 2 should be next in the queue.
}
```

## Recommendation

Don't step the queue when closing a credit. Refactor the call to `stepQ` out of the `_repay` function and call `stepQ` only when repaying the principal debt of a credit that was borrowed.
