# Potential DoS when closing a credit nominated in ETH in the LineOfCredit contract

When closing a credit that was issued in ETH, the LineOfCredit contract will send the lender his deposit and any accrued interests using the `address.transfer(amount)` function, which may fail and revert the whole function, leading to an eventual DoS.

## Impact

The `_close` function in the LineOfCredit contract (called from the `depositAndClose` and `close` functions) will repay the lender his deposit and interests and send the payment using the `LineLib.sendOutTokenOrETH` function, which (in the case of ETH) will execute `address.transfer(amount)` to forward the ETH payment. 

If the lender is a contract account, then this transfer function may fail and cause the close call to revert entirely, leading to a DoS and the impossibility of calling this function successfully. The borrower won't be able to close the credit and the line will never reach a repaid status, which may eventually lead to a liquidatable status and other negative consequences for the borrower.

There are various reason to which this may fail and block the claiming procedure: a) the lender can revert on purpose, b) have an unexpected error on its logic or c) run out of gas (which is particularly relevant since transfer has a limit of just 2300 units of gas)

See https://www.kingoftheether.com/postmortem.html for a detailed description of a similar issue.

## PoC

In the following test, the lender is a contract that creates a credit in the LineOfCredit. The borrower will borrow from this credit, and when trying to repay it and send the ETH back to the lender, the contract will consume all available gas (for demo purposes, in reality this may be a function that inadvertently consumes a lot of gas) and will revert the whole process.

Note: the context for this test (setup, variables and helper functions) is similar to the one found in the file `LineOfCredit.t.sol`.

```
contract BrokenLender {
    function addCredit(ILineOfCredit line, uint256 amount) external {
        line.addCredit{value: amount}(100, 1, amount, Denominations.ETH, address(this));
    }
    
    fallback() external payable {
        // consume gas for demo purposes
        while(true) {}
    }
}

function test_depositAndClose_Lender_DoS() public {        
    uint256 amount = 1 ether;
    BrokenLender lenderContract = new BrokenLender();
    vm.deal(address(lenderContract), amount);
    vm.deal(borrower, 100 ether);
    
    // Setup credit        
    vm.prank(borrower);
    line.addCredit(dRate, fRate, amount, Denominations.ETH, address(lenderContract));
    lenderContract.addCredit(line, amount);
    
    bytes32 id = CreditLib.computeId(address(line), address(lenderContract), Denominations.ETH);
    
    vm.prank(borrower);
    line.borrow(id, amount);
    
    vm.warp(block.timestamp + 30 days);
    
    // Borrower tries to repay credit but it will fail because lender reverts
    vm.expectRevert();
    vm.prank(borrower);
    line.depositAndClose{value: 2 ether}(); // send 2 eth to make sure amount covers principal + debt
}
```

### Recommendation

Use a "pull" pattern where the responsibility of transferring the ETH is shifted to the receiver instead of making it part of the whole process (see https://fravoll.github.io/solidity-patterns/pull_over_push.html). 

In this case, instead of sending the ETH during the repayment procedure, the credit can be marked as closed and let the lender request the ETH payment later at a different stage.

Another alternative would be to use the wrapped ERC20 version WETH instead of native ETH. Note that some variants of ERC20 have callbacks too and can lead to the same scenario.
