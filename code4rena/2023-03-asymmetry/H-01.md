# `ethPerDerivative` can yield different prices for the Reth derivative during the call to `stake`

## Impact

The `stake` function present in the `SafEth` contract accepts user deposits and stakes them into the different derivatives. To calculate the user shares in the vault, the function will first calculate the total ETH value for all the current staked assets and then it will calculate how much ETH resulted after splitting by weight the deposit into the different derivatives. Both of these calculations use the `ethPerDerivative` function of each derivative.

To calculate the total value in ETH for the current staked assets (`underlyingValue`), the implementation will loop each derivative and multiply its balance by the result of `ethPerDerivative` (that returns the current ETH value for one unit, 1e18, of the derivative asset). Note that here we are passing **the total balance of the derivative** as the argument to the `ethPerDerivative` function.

https://github.com/code-423n4/2023-03-asymmetry/blob/main/contracts/SafEth/SafEth.sol#L68-L75

```solidity
uint256 underlyingValue = 0;

// Getting underlying value in terms of ETH for each derivative
for (uint i = 0; i < derivativeCount; i++)
    underlyingValue +=
        (derivatives[i].ethPerDerivative(derivatives[i].balance()) *
            derivatives[i].balance()) /
        10 ** 18;
```

To calculate the ETH value of the user deposit (`totalStakeValueEth`), the function also loops each derivative, deposits the corresponding weighted proportion and then multiplies the deposited amount by the return value of `ethPerDerivative`. Note that here the argument to the `ethPerDerivative` function is **the user deposited amount**.

https://github.com/code-423n4/2023-03-asymmetry/blob/main/contracts/SafEth/SafEth.sol#L83-L96

```solidity
uint256 totalStakeValueEth = 0; // total amount of derivatives worth of ETH in system
for (uint i = 0; i < derivativeCount; i++) {
    uint256 weight = weights[i];
    IDerivative derivative = derivatives[i];
    if (weight == 0) continue;
    uint256 ethAmount = (msg.value * weight) / totalWeight;

    // This is slightly less than ethAmount because slippage
    uint256 depositAmount = derivative.deposit{value: ethAmount}();
    uint derivativeReceivedEthValue = (derivative.ethPerDerivative(
        depositAmount
    ) * depositAmount) / 10 ** 18;
    totalStakeValueEth += derivativeReceivedEthValue;
}
```

The implementation finally calculates the shares to mint by taking the proportion of the user staked value over the total staked value. Here `preDepositPrice` is the `underlyingValue` divided by the `totalSupply` of the shares. Working out the math, this calculation ends up being effectively the canonical formula to calculate shares in a pool, `depositAmount * totalSupply / totalAssets`.

https://github.com/code-423n4/2023-03-asymmetry/blob/main/contracts/SafEth/SafEth.sol#L98

```solidity
uint256 mintAmount = (totalStakeValueEth * 10 ** 18) / preDepositPrice;
```

Now, this behavior is particularly concerning with the Reth implementation of the `ethPerDerivative` function as its implementation is not constant. In this derivative, the result will depend on the amount parameter, as it switches between using the reference conversion from the Rocket Pool contracts and the spot price of the Uniswap V3 pool.

https://github.com/code-423n4/2023-03-asymmetry/blob/main/contracts/SafEth/derivatives/Reth.sol#L211-L216

```solidity
function ethPerDerivative(uint256 _amount) public view returns (uint256) {
    if (poolCanDeposit(_amount))
        return
            RocketTokenRETHInterface(rethAddress()).getEthValue(10 ** 18);
    else return (poolPrice() * 10 ** 18) / (10 ** 18);
}
```

The issue here is that the total value in ETH for the current staked assets (`underlyingValue`) can be calculated with a different conversion rate than the ETH value of the user deposit (`totalStakeValueEth`). During the calculation of `underlyingValue` the argument to `ethPerDerivative` is **the entire Reth balance** of the protocol, while the calculation of `totalStakeValueEth` uses just the user deposit (which can be as low as the weighted fraction of the minimum deposit 0.5 ETH). The `poolCanDeposit` runs some checks based on the current state of the Rocket Pool protocol (for example, check that the amount doesn't exceed the maximum deposit value the protocol allows) and it is completely feasible to return false on a large amount and true for a small amount. 

Given this scenario, the user will either be incorrectly minted less or more shares, depending on the current conversion rates from the Rocket Pool protocol and the Uniswap V3 pool. If the rate of Rocket Pool is higher than the Uniswap V3 rate, the user will be minted more shares, as their deposit value will be using a higher rate. If the rate of Rocket Pool is lower than the Uniswap V3 rate, the used will be minted less shares, as their deposit value will be using a lower rate.

## Proof of Concept

Example scenario to describe the issue:

- Rocket Pool maximum deposit value is 1000 ETH.
- Rocket Pool current deposit value is 950 ETH.
- SafEth current balance for Reth derivative is 100 ETH.
- SafEth weights are equal for all derivatives (meaning user deposit value will be equally distributed).

Steps:

1. User stakes 1.5 ETH
2. `underlyingValue` will be calculated using the Uniswap V3 pool rate as `poolCanDeposit` will return false since `950 + 100 > 1000`.
3. `totalStakeValueEth` will be calculated using the Rocket Pool exchange rate, as the deposit amount for the Reth derivative will be 0.5 ETH (1.5 ETH divided by the 3 implemented derivatives) and `poolCanDeposit` will return true since `950 + 0.5 <= 1000`. 
4. Shares calculation will end up using different valuations (user deposit vs total deposits) for the Reth derivative.

## Recommendation

The `stake` function should ensure to use the same value of `ethPerDerivative` for both calculations, the total value in ETH for the current staked assets (`underlyingValue`) and the ETH value of the user deposit (`totalStakeValueEth`). An easy solution would be to use the user deposit value during both calls to `ethPerDerivative`.
