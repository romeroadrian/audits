# Reth withdrawal may fail due to insufficient collateral

## Impact 

Withdrawals in the Reth derivative are handled directly by the Rocket Pool protocol. The `withdraw` function calls the `burn` function of the RETH token contract:

https://github.com/code-423n4/2023-03-asymmetry/blob/main/contracts/SafEth/derivatives/Reth.sol#L107-L114

```solidity
function withdraw(uint256 amount) external onlyOwner {
    RocketTokenRETHInterface(rethAddress()).burn(amount);
    // solhint-disable-next-line
    (bool sent, ) = address(msg.sender).call{value: address(this).balance}(
        ""
    );
    require(sent, "Failed to send Ether");
}
```

Expanding on the `burn` function we can see that its functionality depends on the availability of collateral to handle the requested amount:

https://github.com/rocket-pool/rocketpool/blob/master/contracts/contract/token/RocketTokenRETH.sol#L106-L123

```solidity
106:     function burn(uint256 _rethAmount) override external {
107:         // Check rETH amount
108:         require(_rethAmount > 0, "Invalid token burn amount");
109:         require(balanceOf(msg.sender) >= _rethAmount, "Insufficient rETH balance");
110:         // Get ETH amount
111:         uint256 ethAmount = getEthValue(_rethAmount);
112:         // Get & check ETH balance
113:         uint256 ethBalance = getTotalCollateral();
114:         require(ethBalance >= ethAmount, "Insufficient ETH balance for exchange");
115:         // Update balance & supply
116:         _burn(msg.sender, _rethAmount);
117:         // Withdraw ETH from deposit pool if required
118:         withdrawDepositCollateral(ethAmount);
119:         // Transfer ETH to sender
120:         msg.sender.transfer(ethAmount);
121:         // Emit tokens burned event
122:         emit TokensBurned(msg.sender, _rethAmount, ethAmount, block.timestamp);
123:     }
```

Line 113 fetches the current collateral balance and line 114 reverts if the available collateral is not enough to cover the requested amount.

This means that protocol actions that withdraw from the Reth derivative may eventually fail and block the whole operation. These actions include the user unstake from SafEth and the rebalancing of the derivatives, the latter being particularly concerning because its implementation will withdraw **all balance** from the Reth derivative.

## Proof of Concept

User unstake scenario:

1. User tries to unstake their current position.
2. Assume calculated share of user position in Reth is greater than the available collateral present in the Rocket Pool protocol.
3. Call to `unstake` will revert because withdraw from Reth will fail.

Rebalancing scenario:

1. Protocol owner calls `rebalanceToWeights`.
2. Assume current total balance in Reth derivative is greater than the available collateral present in the Rocket Pool protocol.
3. Call to `rebalanceToWeights` will revert because withdraw from Reth will fail.

## Recommendation

Similar to how other derivatives work, the Reth implementation can fallback to swapping assets in the Uniswap pool. The `withdraw` function can query the current available collateral in the Rocket Pool protocol. If the requested withdrawal amount is greater than the available collateral, the function can exchange the remaining portion using the Uniswap pool.

