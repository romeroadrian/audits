# Existing pools will be bricked due to uninitialized state

## Summary

Pools already present in the exchange will be bricked when `crossTicks()` is called with an uninitialized `tickTracking_` storage, which will trigger an array out of bounds error.

## Impact

New pools in the Ambient DEX have their tick tracking storage initialized in the `initTickTracking()` function:

https://github.com/code-423n4/2023-10-canto/blob/main/canto_ambient/contracts/mixins/LiquidityMining.sol#L16-L20

```solidity
16:     function initTickTracking(bytes32 poolIdx, int24 tick) internal {
17:         StorageLayout.TickTracking memory tickTrackingData = StorageLayout
18:             .TickTracking(uint32(block.timestamp), 0);
19:         tickTracking_[poolIdx][tick].push(tickTrackingData);
20:     }
```

This function is called as part of a call chain that is initiated in `ColdPath::initPool()` when a pool is created. Note that in line 19, the initial pool tick is inserted in the `tickTracking_` array.

The core tick tracking is then implemented in the function `crossTicks()`:

https://github.com/code-423n4/2023-10-canto/blob/main/canto_ambient/contracts/mixins/LiquidityMining.sol#L24-L35

```solidity
24:     function crossTicks(
25:         bytes32 poolIdx,
26:         int24 exitTick,
27:         int24 entryTick
28:     ) internal {
29:         uint256 numElementsExit = tickTracking_[poolIdx][exitTick].length;
30:         tickTracking_[poolIdx][exitTick][numElementsExit - 1]
31:             .exitTimestamp = uint32(block.timestamp);
32:         StorageLayout.TickTracking memory tickTrackingData = StorageLayout
33:             .TickTracking(uint32(block.timestamp), 0);
34:         tickTracking_[poolIdx][entryTick].push(tickTrackingData);
35:     }
```

Here, in lines 29-31, we can see that the implementation sets exit timestamp on the last element of `exitTick`. This, at first, looks fine since the `exitTick` must have previously entered at some point: the first time is covered by `initTickTracking()`, and afterwards this entry gets inserted as part of `crossTicks()` (line 34). This means that there should be at least one entry in the array.

However, this is not the case with pools that already exist in the exchange. These pools are already created and initialized, meaning the new `initTickTracking()` function won't be called, thus leaving an uninitialized `tickTracking_` structure. This also means that `crossTicks()` will try to fetch the last element of an empty array. The previous assumption is not valid in this case.

Eventually, when  `crossTicks()` gets called on these pools, as part of a normal operation such as a swap, the implementation will try to execute `numElementsExit - 1 = 0 - 1 = -1` and revert with an underflow, bricking the process.

## Proof of Concept

Let's assume there is an existing pool in the Ambient protocol.

1. New code containing the liquidity mining feature is deployed.
2. A user tries to execute a swap in the pool such that the action shift the price to a different tick (i.e. meaning `postTradeTick` will be different than `preTradeTick` in TradeMatcher contract, see https://github.com/code-423n4/2023-10-canto/blob/main/canto_ambient/contracts/mixins/TradeMatcher.sol#L441-L443).
3. The function `crossTicks()` in LiquidityMining is called. 
4. The array for `preTradeTick` will be empty, since nothing was initialized. This means `numElementsExit = tickTracking_[poolIdx][exitTick].length = 0`.
5. The expression `tickTracking_[poolIdx][exitTick][numElementsExit - 1]` will cause an underflow, `tickTracking_[poolIdx][exitTick][numElementsExit - 1]`, since `numElementsExit - 1 = 0 - 1`, reverting the transaction.

## Recommendation

A quick solution would be to avoid setting the `exitTimestamp` if there isn't an existing entry for the `exitTick`.

```diff
    function crossTicks(
        bytes32 poolIdx,
        int24 exitTick,
        int24 entryTick
    ) internal {
        uint256 numElementsExit = tickTracking_[poolIdx][exitTick].length;
+       if (numElementsExit > 0)
            tickTracking_[poolIdx][exitTick][numElementsExit - 1]
                .exitTimestamp = uint32(block.timestamp);
+       }
        StorageLayout.TickTracking memory tickTrackingData = StorageLayout
            .TickTracking(uint32(block.timestamp), 0);
        tickTracking_[poolIdx][entryTick].push(tickTrackingData);
    }
```

It should be also noted that this might also contain edge cases. Ideally, a proper solution would require a dedicated process to initialize existing pool already present in the exchange.
