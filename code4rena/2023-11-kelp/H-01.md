# Mint amount calculation in deposit is incorrect

## Summary

The calculation in the deposit function of the DepositPool contract is flawed as it factors the deposited amount into the RSETH price to calculate the amount to mint.

## Impact

When a user deposits in the DepositPool contract, the amount of RSETH to mint is determined by the price returned by the LRTOracle:

https://github.com/code-423n4/2023-11-kelp/blob/f751d7594051c0766c7ecd1e68daeb0661e43ee3/src/LRTOracle.sol#L52-L79

```solidity
52:     function getRSETHPrice() external view returns (uint256 rsETHPrice) {
53:         address rsETHTokenAddress = lrtConfig.rsETH();
54:         uint256 rsEthSupply = IRSETH(rsETHTokenAddress).totalSupply();
55: 
56:         if (rsEthSupply == 0) {
57:             return 1 ether;
58:         }
59: 
60:         uint256 totalETHInPool;
61:         address lrtDepositPoolAddr = lrtConfig.getContract(LRTConstants.LRT_DEPOSIT_POOL);
62: 
63:         address[] memory supportedAssets = lrtConfig.getSupportedAssetList();
64:         uint256 supportedAssetCount = supportedAssets.length;
65: 
66:         for (uint16 asset_idx; asset_idx < supportedAssetCount;) {
67:             address asset = supportedAssets[asset_idx];
68:             uint256 assetER = getAssetPrice(asset);
69: 
70:             uint256 totalAssetAmt = ILRTDepositPool(lrtDepositPoolAddr).getTotalAssetDeposits(asset);
71:             totalETHInPool += totalAssetAmt * assetER;
72: 
73:             unchecked {
74:                 ++asset_idx;
75:             }
76:         }
77: 
78:         return totalETHInPool / rsEthSupply;
79:     }
```

In summary, the implementation takes all assets held by the protocol and normalizes them into ETH. This value is then divided by the current supply of RSETH to get the price per token. For each asset, deposits are given by the `getTotalAssetDeposits()` function:

https://github.com/code-423n4/2023-11-kelp/blob/f751d7594051c0766c7ecd1e68daeb0661e43ee3/src/LRTDepositPool.sol#L47-L51

```solidity
47:     function getTotalAssetDeposits(address asset) public view override returns (uint256 totalAssetDeposit) {
48:         (uint256 assetLyingInDepositPool, uint256 assetLyingInNDCs, uint256 assetStakedInEigenLayer) =
49:             getAssetDistributionData(asset);
50:         return (assetLyingInDepositPool + assetLyingInNDCs + assetStakedInEigenLayer);
51:     }
```

https://github.com/code-423n4/2023-11-kelp/blob/f751d7594051c0766c7ecd1e68daeb0661e43ee3/src/LRTDepositPool.sol#L71-L89

```solidity
71:     function getAssetDistributionData(address asset)
72:         public
73:         view
74:         override
75:         onlySupportedAsset(asset)
76:         returns (uint256 assetLyingInDepositPool, uint256 assetLyingInNDCs, uint256 assetStakedInEigenLayer)
77:     {
78:         // Question: is here the right place to have this? Could it be in LRTConfig?
79:         assetLyingInDepositPool = IERC20(asset).balanceOf(address(this));
80: 
81:         uint256 ndcsCount = nodeDelegatorQueue.length;
82:         for (uint256 i; i < ndcsCount;) {
83:             assetLyingInNDCs += IERC20(asset).balanceOf(nodeDelegatorQueue[i]);
84:             assetStakedInEigenLayer += INodeDelegator(nodeDelegatorQueue[i]).getAssetBalance(asset);
85:             unchecked {
86:                 ++i;
87:             }
88:         }
89:     }
```

The implementation sums the assets owned by the DepositPool, as well as the balance and staked amount in Eigenlayer for each node delegator.

The RSETH price is then used to calculate the minting amount in `depositAsset()`:

https://github.com/code-423n4/2023-11-kelp/blob/f751d7594051c0766c7ecd1e68daeb0661e43ee3/src/LRTDepositPool.sol#L119-L144

```solidity
119:     function depositAsset(
120:         address asset,
121:         uint256 depositAmount
122:     )
123:         external
124:         whenNotPaused
125:         nonReentrant
126:         onlySupportedAsset(asset)
127:     {
128:         // checks
129:         if (depositAmount == 0) {
130:             revert InvalidAmount();
131:         }
132:         if (depositAmount > getAssetCurrentLimit(asset)) {
133:             revert MaximumDepositLimitReached();
134:         }
135: 
136:         if (!IERC20(asset).transferFrom(msg.sender, address(this), depositAmount)) {
137:             revert TokenTransferFailed();
138:         }
139: 
140:         // interactions
141:         uint256 rsethAmountMinted = _mintRsETH(asset, depositAmount);
142: 
143:         emit AssetDeposit(asset, depositAmount, rsethAmountMinted);
144:     }
```

```solidity
151:     function _mintRsETH(address _asset, uint256 _amount) private returns (uint256 rsethAmountToMint) {
152:         (rsethAmountToMint) = getRsETHAmountToMint(_asset, _amount);
153: 
154:         address rsethToken = lrtConfig.rsETH();
155:         // mint rseth for user
156:         IRSETH(rsethToken).mint(msg.sender, rsethAmountToMint);
157:     }
```

```solidity
095:     function getRsETHAmountToMint(
096:         address asset,
097:         uint256 amount
098:     )
099:         public
100:         view
101:         override
102:         returns (uint256 rsethAmountToMint)
103:     {
104:         // setup oracle contract
105:         address lrtOracleAddress = lrtConfig.getContract(LRTConstants.LRT_ORACLE);
106:         ILRTOracle lrtOracle = ILRTOracle(lrtOracleAddress);
107: 
108:         // calculate rseth amount to mint based on asset amount and asset exchange rate
109:         rsethAmountToMint = (amount * lrtOracle.getAssetPrice(asset)) / lrtOracle.getRSETHPrice();
110:     }
```

`getRsETHAmountToMint()` takes the deposit amount, multiplies it by the asset price (to normalize it to ETH) and divides it by the previously mentioned RSETH price calculation. However, the implementation of `depositAsset()` **transfers first the deposit into the contract before fetching the current price**. Line 136 pulls the tokens from the user into the contract and the call in line 141 does the calculation. This means the RSETH price calculation, which is based on the amount of assets held by the protocol (including the ones in the DepositPool), will be affected by the same transfer that happens in the function.

As balances are increased by the user's tokens, the RSETH price will be increased too (since the supply is still the same), which means that the user will be minted less RSETH tokens than expected.

## Proof of Concept

In the following test, Alice first deposits `1e18` stETH tokens. As this is the first deposit in the pool, she is minted `1e18` shares of RSETH. Now, Bob deposits the same amount of stETH tokens but he just gets `0.5e18` RSETH tokens, half of what Alice received for depositing the same amount. 

The difference comes from the fact that Bob's tokens are pulled into the contract before calculating the price, effectively duplicating the price of RSETH. Once the price is doubled, Bob is minted half of what it should be.

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/6369f83ec39bf473fd7c40af3914ac39).

```solidity
function test_DepositPool_WrongMintCalculation() public {
    uint256 depositAmount = 1e18;

    // Alice deposits 1e18 stETH
    vm.startPrank(alice);

    stETH.approve(address(lrtDepositPool), type(uint256).max);

    lrtDepositPool.depositAsset(address(stETH), depositAmount);

    vm.stopPrank();

    // Alice is minted 1e18 RSETH
    assertEq(rseth.balanceOf(alice), 1e18);

    // Now, Bob deposits the same amount
    vm.startPrank(bob);

    stETH.approve(address(lrtDepositPool), type(uint256).max);

    lrtDepositPool.depositAsset(address(stETH), depositAmount);

    vm.stopPrank();

    // Bob is minted just 0.5e18 RSETH when he deposited the same amount as Alice
    assertEq(rseth.balanceOf(bob), 0.5e18);
}
```

## Recommendation

The RSETH price calculation should not factor the tokens that are part of the deposit. Either, snapshot the price before pulling the tokens, or simply switch the minting order so that tokens are pulled after the mint:

```diff
+   // interactions
+   uint256 rsethAmountMinted = _mintRsETH(asset, depositAmount);

    if (!IERC20(asset).transferFrom(msg.sender, address(this), depositAmount)) {
        revert TokenTransferFailed();
    }

-   // interactions
-   uint256 rsethAmountMinted = _mintRsETH(asset, depositAmount);

    emit AssetDeposit(asset, depositAmount, rsethAmountMinted);
```
