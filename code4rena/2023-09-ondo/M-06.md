# Oracle could potentially return an invalid zero price

The implementation of the `getPrice()` function allows a fallback that returns zero if no range can be found.

## Impact

The `getPrice()` function provides the main functionality for retrieving the current price in the oracle contract. This is achieving by looping the ranges in reverse order to find the last range that has a start timestamp previous to the current timestamp:

https://github.com/code-423n4/2023-09-ondo/blob/main/contracts/rwaOracles/RWADynamicOracle.sol#L75-L87

```solidity
75:   function getPrice() public view whenNotPaused returns (uint256 price) {
76:     uint256 length = ranges.length;
77:     for (uint256 i = 0; i < length; ++i) {
78:       Range storage range = ranges[(length - 1) - i];
79:       if (range.start <= block.timestamp) {
80:         if (range.end <= block.timestamp) {
81:           return derivePrice(range, range.end - 1);
82:         } else {
83:           return derivePrice(range, block.timestamp);
84:         }
85:       }
86:     }
87:   }
```

If none of the configured ranges matches the condition in line 79, which can happen for example if all of the ranges have a start timestamp that is in the future, the implementation will fallthrough the loop and exit. Note also that the function provides a named return using the variable `price`, which means that the value of this variable will be used in case the function exits without an explicit return statement. Since this variable is never written, it will have its default value which is zero. This means that if none of the ranges match the condition, `getPrice()` will execute correctly and return zero.

## Proof of Concept

1. Configured ranges have a `range.start` timestamp that is in the future.
2. Calling `getPrice()` will loop through all the ranges but fail to find any match.
3. Function will return zero due to the uninitialized named return.

## Recommended Mitigation Steps

Revert in case none of the configured ranges match the condition:

```diff
  function getPrice() public view whenNotPaused returns (uint256 price) {
    uint256 length = ranges.length;
    for (uint256 i = 0; i < length; ++i) {
      Range storage range = ranges[(length - 1) - i];
      if (range.start <= block.timestamp) {
        if (range.end <= block.timestamp) {
          return derivePrice(range, range.end - 1);
        } else {
          return derivePrice(range, block.timestamp);
        }
      }
    }
+   revert();
  }
```
