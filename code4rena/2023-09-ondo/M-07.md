# Overriding ranges in oracle may lead to gaps

The override feature in the RWADynamicOracle contract can lead to gaps in time without provided values.

## Impact

Already configured ranges in the oracle contract can be re-configured using the `overrideRange()` function. This function takes the index of the range to modify along with the new values to configure in the corresponding range:

https://github.com/code-423n4/2023-09-ondo/blob/main/contracts/rwaOracles/RWADynamicOracle.sol#L186-L236

```solidity
186:   function overrideRange(
187:     uint256 indexToModify,
188:     uint256 newStart,
189:     uint256 newEnd,
190:     uint256 newDailyIR,
191:     uint256 newPrevRangeClosePrice
192:   ) external onlyRole(DEFAULT_ADMIN_ROLE) {
193:     // Check that the ranges start and end time are less than each other
194:     if (newStart >= newEnd) revert InvalidRange();
195: 
196:     uint256 rangeLength = ranges.length;
197:     // Case 1: The range being modified is the first range
198:     if (indexToModify == 0) {
199:       // If the length of ranges is greater than 1,
200:       // Ensure that the newEnd time is not greater than the start time of the next range
201:       if (rangeLength > 1 && newEnd > ranges[indexToModify + 1].start)
202:         revert InvalidRange();
203:     }
204:     // Case 2: The range being modified is the last range
205:     else if (indexToModify == rangeLength - 1) {
206:       // Ensure that the newStart time is not less than the end time of the previous range
207:       if (newStart < ranges[indexToModify - 1].end) revert InvalidRange();
208:     }
209:     // Case 3: The range being modified is between first and last range
210:     else {
211:       // Ensure that the newStart time is less than the end time of the previous range
212:       if (newStart < ranges[indexToModify - 1].end) revert InvalidRange();
213:       // Ensure that the newEnd time is not greater than the start time of the next range
214:       if (newEnd > ranges[indexToModify + 1].start) revert InvalidRange();
215:     }
216: 
217:     // Update range
218:     if (indexToModify == 0) {
219:       uint256 trueStart = (newPrevRangeClosePrice * ONE) / newDailyIR;
220:       ranges[indexToModify] = Range(newStart, newEnd, newDailyIR, trueStart);
221:     } else {
222:       ranges[indexToModify] = Range(
223:         newStart,
224:         newEnd,
225:         newDailyIR,
226:         newPrevRangeClosePrice
227:       );
228:     }
229:     emit RangeOverriden(
230:       indexToModify,
231:       newStart,
232:       newEnd,
233:       newDailyIR,
234:       newPrevRangeClosePrice
235:     );
236:   }
```

While the implementation correctly validates that the new timestamps don't exceed the limits of the adjacent ranges (lines 197-215), there is nothing preventing these values from being lower than the adjacent timestamp, causing gaps in time. For example, line 212 validates that the start time isn't before the end time of the previous range, but allows to set a greater start time leading to a gap between the previous range end time and the modified range start time. Note also that there's no functionality to insert a new range between two existing ranges, which means that these gaps can't be filled either. 

## Proof of Concept

Let's say the admin wants to modify range `N`. Range `N-1` has an end timestamp of `t1` and range `N+1` has a start timestamp of `t3`. Timestamp `t2` is between `t1` and `t3` such that `t1 < t2 < t3`.

1. Admins calls `overrideRange(N, t2, t3, newDailyIR, newPrevRangeClosePrice)`.
2. Validations are ok since `t2 >= t1` and `t3 <= t3`.
3. This causes a gap between `t1` and `t2`. Range `N-1` will end in `t1` but range N will start at `t2`.

## Recommended Mitigation Steps

The issue can be solved by simplifying the range implementation. Instead of storing both start and end timestamp for a range, simply store the start of it and have its end time represented by the next range start time. This could solve the gap issue, while still allowing ranges to be overridden using simple validations. 


