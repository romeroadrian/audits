# Rounding issues in rUSDY calculations

Conversion between shares and rUSDY amounts always round down in the rUSDY, which may not be the appropriate calculation in some cases.

## Impact

The `getSharesByRUSDY()` function present in the rUSDY contract can be used to convert between rUSDY tokens and shares.

https://github.com/code-423n4/2023-09-ondo/blob/main/contracts/usdy/rUSDY.sol#L388-L392

```solidity
388:   function getSharesByRUSDY(
389:     uint256 _rUSDYAmount
390:   ) public view returns (uint256) {
391:     return (_rUSDYAmount * 1e18 * BPS_DENOMINATOR) / oracle.getPrice();
392:   }
```

The implementation contains a division that, under integer arithmetic, will round down the resulting value. This rounding will cause different issues in different places of the rUSDY token implementation.

One of these issues is in the token transfer implementation. Both `transfer()` and `transferFrom()` calculate the needed shares using `getSharesByRUSDY()` and execute a movement of shares between both accounts:

https://github.com/code-423n4/2023-09-ondo/blob/main/contracts/usdy/rUSDY.sol#L463-L472

```solidity
463:   function _transfer(
464:     address _sender,
465:     address _recipient,
466:     uint256 _amount
467:   ) internal {
468:     uint256 _sharesToTransfer = getSharesByRUSDY(_amount);
469:     _transferShares(_sender, _recipient, _sharesToTransfer);
470:     emit Transfer(_sender, _recipient, _amount);
471:     emit TransferShares(_sender, _recipient, _sharesToTransfer);
472:   }
```

A rounded down value in the shares being transferred means that the recipient may not get the specified amount. There will be a slight difference due to the rounded down shares causing the recipient to actually receive less than specified.

Another issue is present in the `unwrap()` function. The implementation takes an amount of rUSDY tokens (`_rUSDYAmount`) and calculates the shares to burn using `getSharesByRUSDY()`. A rounded down value here means that the user may not be able to fully redeem the entirety of their shares.

https://github.com/code-423n4/2023-09-ondo/blob/main/contracts/usdy/rUSDY.sol#L449-L456

```solidity
449:   function unwrap(uint256 _rUSDYAmount) external whenNotPaused {
450:     require(_rUSDYAmount > 0, "rUSDY: can't unwrap zero rUSDY tokens");
451:     uint256 usdyAmount = getSharesByRUSDY(_rUSDYAmount);
452:     if (usdyAmount < BPS_DENOMINATOR) revert UnwrapTooSmall();
453:     _burnShares(msg.sender, usdyAmount);
454:     usdy.transfer(msg.sender, usdyAmount / BPS_DENOMINATOR);
455:     emit TokensBurnt(msg.sender, _rUSDYAmount);
456:   }
```

Finally there is also a potential issue in `burn()`. Once again, `getSharesByRUSDY()` is used to calculate the amount of shares corresponding to the amount of rUSDY token that should be burned. A rounded down value here means that less shares are actually burned, meaning that the account's balance will be reduced less than expected.

https://github.com/code-423n4/2023-09-ondo/blob/main/contracts/usdy/rUSDY.sol#L672-L683

```solidity
672:   function burn(
673:     address _account,
674:     uint256 _amount
675:   ) external onlyRole(BURNER_ROLE) {
676:     uint256 sharesAmount = getSharesByRUSDY(_amount);
677: 
678:     _burnShares(_account, sharesAmount);
679: 
680:     usdy.transfer(msg.sender, sharesAmount / BPS_DENOMINATOR);
681: 
682:     emit TokensBurnt(_account, _amount);
683:   }
```

## Recommended Mitigation Steps

The issue in `unwrap()` can be simply solved by changing the specification to receive the amount of shares to be unwrapped instead of the amount of rUSDY tokens. This will solve the issue and provide a cleaner implementation for this function.

For `burn()`, the implementation can round the calculation up to ensure no less than the specified amount is actually burned.

The transfer case is a bit more difficult as the current design of the token cannot represent an exact value of rUSDY in the form of shares. In any case, rounding up the calculation would probably provide a better implementation, as the transferred value will be _at least_ the specified amount, and not less.


