# Approver could accidentally DoS their approval by executing the bridged message

Transaction approvals in the destination bridge are registered by address to avoid the same approver from approving it more than once. It is possible that a registered approver executes the message in the destination bridge accidentally including themselves in the list of approvers and preventing them from making the "real" approval.

## Impact

Approval semantics in the destination bridge are a bit confusing. Bridged messages grant an "automatic" approval when the message is being executed in the destination bridge:

https://github.com/code-423n4/2023-09-ondo/blob/main/contracts/bridge/DestinationBridge.sol#L108-L113

```solidity
108:     bytes32 txnHash = keccak256(payload);
109:     txnHashToTransaction[txnHash] = Transaction(srcSender, amt);
110:     _attachThreshold(amt, txnHash, srcChain);
111:     _approve(txnHash);
112:     _mintIfThresholdMet(txnHash);
113:     emit MessageReceived(srcChain, srcSender, amt, nonce);
```

This means that any bridged message will get at least one automatic approval. This seems to be designed to avoid defining thresholds with a zero value for `numberOfApprovalsNeeded` to avoid any potential conflict with an uninitialized setting (since the default value for an integer is zero). This also means that messages that require an "actual" approval will need its threshold config to be set to (at least) 2 as the number of required approvals.

Approvals are of course checked for duplicates to avoid the same approver from approving the message multiple times:

https://github.com/code-423n4/2023-09-ondo/blob/main/contracts/bridge/DestinationBridge.sol#L156-L167

```solidity
156:   function _approve(bytes32 txnHash) internal {
157:     // Check that the approver has not already approved
158:     TxnThreshold storage t = txnToThresholdSet[txnHash];
159:     if (t.approvers.length > 0) {
160:       for (uint256 i = 0; i < t.approvers.length; ++i) {
161:         if (t.approvers[i] == msg.sender) {
162:           revert AlreadyApproved();
163:         }
164:       }
165:     }
166:     t.approvers.push(msg.sender);
167:   }
```

Since bridged messages can be executed by anyone (the `execute()` function in AxelarExecutable is permissionless), this means that it could be executed by an approver, which would cause him to be added to the list of approvers due to the automatic call to `_approve()` in `_execute()`, locking him out of making the "real" approval.

## Proof of Concept

1. Approver calls `execute()` in DestinationBridge.
2. Approver tries to call `approve()` to grant their approval, but it will revert since the approver is already included in the list of approvers.

## Recommended Mitigation Steps

Avoid the confusing semantics by allowing the number of required approvals to be zero. This would allow to remove the automatic call to `_approve()` that is part of the implementation of `_execute()`. The uninitialized configuration issue could be solved by slightly changing `_attachThreshold()` to revert if no threshold if found:

```diff
  function _attachThreshold(
    uint256 amount,
    bytes32 txnHash,
    string memory srcChain
  ) internal {
    Threshold[] memory thresholds = chainToThresholds[srcChain];
    for (uint256 i = 0; i < thresholds.length; ++i) {
      Threshold memory t = thresholds[i];
      if (amount <= t.amount) {
        txnToThresholdSet[txnHash] = TxnThreshold(
          t.numberOfApprovalsNeeded,
          new address[](0)
        );
+        return;
-        break;
      }
    }
-    if (txnToThresholdSet[txnHash].numberOfApprovalsNeeded == 0) {
      revert NoThresholdMatch();
-    }
  }
```

This should improve the semantics of transaction approvals and solve the described issue, while also improving gas costs.
