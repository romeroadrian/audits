# PluginSetupProcessor cannot upgrade plugins with current architecture

## Impact

The `PluginSetupProcessor` contract is responsible for managing installations, updates and removals of plugins in the context of DAO. 

The proposed plugin update procedure and implementation is incompatible with the current architecture and plugin setup, since the `PluginSetupProcessor` lacks the required permissions to execute the upgrade. From the [docs](https://devs.aragon.org/docs/osx/how-it-works/framework/plugin-management/plugin-setup/#setup-application):

>1. The DAO temporarily grants the ROOT_PERMISSION_ID permission to the PluginSetupProcessor. This is needed so that the processor can modify the DAO's permissions settings to setup the plugin.
>2. The next Action calls the processInstallation, processUpdate, or processUninstallation method in the PluginSetupProcessor depending on the setup process and with the permission list as an argument. The permission hash is compared with the stored hash to make sure that the permission didnâ€™t change. In addition to the above, the update process also upgrades the logic contract to which the proxy points too.
> 3. If the hash is valid, the list is processed and PluginSetupProcessor conducts the requested sequence of grant, grantWithCondition and revoke calls on the owning DAO. Finally, the PluginSetupProcessor asks the DAO to revoke the ROOT_PERMISSION_ID permission from itself.

Even though the process grants the `ROOT_PERMISSION_ID` to the `PluginSetupProcessor`, the `applyUpdate` function will also require the `UPGRADE_PLUGIN_PERMISSION_ID` as the call to upgrade the plugin happens within the `PluginSetupProcessor` contract (i.e. caller is `PluginSetupProcessor`).

https://github.com/code-423n4/2023-03-aragon/blob/main/packages/contracts/src/core/plugin/PluginUUPSUpgradeable.sol#L61-L63

```solidity
function _authorizeUpgrade(
    address
) internal virtual override auth(UPGRADE_PLUGIN_PERMISSION_ID) {}
```

https://github.com/code-423n4/2023-03-aragon/blob/main/packages/contracts/src/framework/plugin/setup/PluginSetupProcessor.sol#L664-L694

```solidity
function _upgradeProxy(
    address _proxy,
    address _implementation,
    bytes memory _initData
) private {
    if (_initData.length > 0) {
        try
            PluginUUPSUpgradeable(_proxy).upgradeToAndCall(_implementation, _initData)
        {} catch Error(string memory reason) {
            revert(reason);
        } catch (bytes memory /*lowLevelData*/) {
            revert PluginProxyUpgradeFailed({
                proxy: _proxy,
                implementation: _implementation,
                initData: _initData
            });
        }
    } else {
        try PluginUUPSUpgradeable(_proxy).upgradeTo(_implementation) {} catch Error(
            string memory reason
        ) {
            revert(reason);
        } catch (bytes memory /*lowLevelData*/) {
            revert PluginProxyUpgradeFailed({
                proxy: _proxy,
                implementation: _implementation,
                initData: _initData
            });
        }
    }
}
```

Therefore, upgradeable plugins will fail to be upgraded with the given setup (see test case below).

## Proof of Concept

In the following test, we bootstrap a DAO and install the `Multisig` plugin using the `PluginSetupProcessor`. We then create a dummy new version of the plugin (`multisigSetupV2`) and try to upgrade it using the proposed actions. The `execute` call will fail as the `PluginSetupProcessor` lacks the required upgrade permission.

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/c3662b7324fd561991bf2ad174052680).

```solidity
function test_PluginSetupProcessor_applyUpdate_UnauthorizedUpgrade() public {
    DAO dao = createDao();

    (address multisigPlugin, IPluginSetup.PreparedSetupData memory preparedSetupData) = installMultisig(dao);

    vm.roll(block.number + 1);

    applyMultisigUpdate(dao, multisigPlugin, preparedSetupData.helpers);
}

function createDao() internal returns (DAO dao) {
    dao = DAO(payable(createERC1967Proxy(address(daoImpl), bytes(""))));
    dao.initialize(
        "", // metadata
        address(this), // initialOwner
        address(0), // trustedForwarder
        "" // daoURI
    );
    dao.grant(address(dao), address(this), dao.EXECUTE_PERMISSION_ID());
    dao.grant(address(dao), address(dao), dao.ROOT_PERMISSION_ID());
}

function installMultisig(DAO dao) internal returns (address multisigPlugin, IPluginSetup.PreparedSetupData memory preparedSetupData) {
    // Prepare installation of Multisig
    address[] memory multisigMembers = new address[](1);
    multisigMembers[0] = address(this);
    Multisig.MultisigSettings memory multisigSettings = Multisig
        .MultisigSettings({onlyListed: true, minApprovals: 1});
    PluginSetupProcessor.PrepareInstallationParams
        memory prepareInstallationParams = PluginSetupProcessor
            .PrepareInstallationParams({
                pluginSetupRef: PluginSetupRef({
                    versionTag: PluginRepo.Tag(1, 1),
                    pluginSetupRepo: multisigRepo
                }),
                data: abi.encode(multisigMembers, multisigSettings)
            });
    (
        multisigPlugin,
        preparedSetupData
    ) = pluginSetupProcessor.prepareInstallation(
            address(dao),
            prepareInstallationParams
        );

    // Create proposal to install
    DAO.Action[] memory installMultisigActions = new DAO.Action[](3);
    installMultisigActions[0].to = address(dao);
    installMultisigActions[0].data = abi.encodeWithSelector(
        PermissionManager.grant.selector,
        address(dao),
        address(pluginSetupProcessor),
        dao.ROOT_PERMISSION_ID()
    );

    PluginSetupProcessor.ApplyInstallationParams
        memory applyInstallationParams = PluginSetupProcessor
            .ApplyInstallationParams({
                pluginSetupRef: PluginSetupRef({
                    versionTag: PluginRepo.Tag(1, 1),
                    pluginSetupRepo: multisigRepo
                }),
                plugin: multisigPlugin,
                permissions: preparedSetupData.permissions,
                helpersHash: hashHelpers(preparedSetupData.helpers)
            });
    installMultisigActions[1].to = address(pluginSetupProcessor);
    installMultisigActions[1].data = abi.encodeWithSelector(
        PluginSetupProcessor.applyInstallation.selector,
        address(dao),
        applyInstallationParams
    );

    installMultisigActions[2].to = address(dao);
    installMultisigActions[2].data = abi.encodeWithSelector(
        PermissionManager.revoke.selector,
        address(dao),
        address(pluginSetupProcessor),
        dao.ROOT_PERMISSION_ID()
    );

    // Execute proposal
    dao.execute(bytes32(0x0), installMultisigActions, 0);
}

function applyMultisigUpdate(
    DAO dao,
    address multisigPlugin,
    address[] memory helpers
) internal {
    // Prepare update
    MultisigSetup multisigSetupV2 = new MultisigSetup();
    multisigRepo.createVersion(1, address(multisigSetupV2), "", "");

    PluginSetupProcessor.PrepareUpdateParams
        memory prepareUpdateParams = PluginSetupProcessor
            .PrepareUpdateParams({
                currentVersionTag: PluginRepo.Tag(1, 1),
                newVersionTag: PluginRepo.Tag(1, 2),
                pluginSetupRepo: multisigRepo,
                setupPayload: IPluginSetup.SetupPayload({
                    plugin: multisigPlugin,
                    currentHelpers: helpers,
                    data: ""
                })
            });
    (
        bytes memory updateInitData,
        IPluginSetup.PreparedSetupData memory updatePreparedSetupData
    ) = pluginSetupProcessor.prepareUpdate(
            address(dao),
            prepareUpdateParams
        );

    // Create proposal to update
    DAO.Action[] memory updateMultisigActions = new DAO.Action[](3);
    updateMultisigActions[0].to = address(dao);
    updateMultisigActions[0].data = abi.encodeWithSelector(
        PermissionManager.grant.selector,
        address(dao),
        address(pluginSetupProcessor),
        dao.ROOT_PERMISSION_ID()
    );

    PluginSetupProcessor.ApplyUpdateParams
        memory applyUpdateParams = PluginSetupProcessor.ApplyUpdateParams({
            plugin: multisigPlugin,
            pluginSetupRef: PluginSetupRef({
                versionTag: PluginRepo.Tag(1, 2),
                pluginSetupRepo: multisigRepo
            }),
            initData: updateInitData,
            permissions: updatePreparedSetupData.permissions,
            helpersHash: hashHelpers(updatePreparedSetupData.helpers)
        });
    updateMultisigActions[1].to = address(pluginSetupProcessor);
    updateMultisigActions[1].data = abi.encodeWithSelector(
        PluginSetupProcessor.applyUpdate.selector,
        address(dao),
        applyUpdateParams
    );

    updateMultisigActions[2].to = address(dao);
    updateMultisigActions[2].data = abi.encodeWithSelector(
        PermissionManager.revoke.selector,
        address(dao),
        address(pluginSetupProcessor),
        dao.ROOT_PERMISSION_ID()
    );

    // Execute proposal
    vm.expectRevert(
        abi.encodeWithSelector(DAO.ActionFailed.selector, 1)
    );
    dao.execute(bytes32(uint256(1)), updateMultisigActions, 0);
}
```

## Recommendation

There are different alternatives to fix the issue:

1. Besides granting the `ROOT_PERMISSION_ID` in the setup actions, grant also the `UPGRADE_PLUGIN_PERMISSION_ID` (and revoke it later).
2. During the call to `applyUpdate` in `PluginSetupProcessor`, the contract can add the required `UPGRADE_PLUGIN_PERMISSION_ID` permission by itself as it has the `ROOT_PERMISSION_ID` (and revoke it after upgrading the proxy).
3. The function `_authorizeUpgrade` in `PluginUUPSUpgradeable` could also allow callers that have the `ROOT_PERMISSION_ID` (so it is authorized in both cases).

## Note from warden

I've discussed this privately with one of the protocol team members, who suggested the finding to be low severity due to the fix being particularly easy. Even though the workarounds are easy to implement, I'm submitting the issue as I still think the discovery is relevant. Feel free to downgrade it.
