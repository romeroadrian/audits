# Percentage calculation could leave unused ETH leftovers in AfEth deposit

## Summary

The logic used to split the deposit amount between SafEth and the Votium strategy can leave some leftovers that will be ignored by the implementation.

## Impact

Deposits in the AfEth contract are split based on a configured `ratio`. One portion of the split goes to SafEth, while the other is deposited in the Votium strategy.

To calculate the split, the implementation multiplies the deposit amount by `ratio` for the SafEth portion, and the deposit amount by `1e18 - ratio` for the VotingStrategy portion.

https://github.com/code-423n4/2023-09-asymmetry/blob/main/contracts/AfEth.sol#L148-L169

```solidity
148:     function deposit(uint256 _minout) external payable virtual {
149:         if (pauseDeposit) revert Paused();
150:         uint256 amount = msg.value;
151:         uint256 priceBeforeDeposit = price();
152:         uint256 totalValue;
153: 
154:         AbstractStrategy vStrategy = AbstractStrategy(vEthAddress);
155: 
156:         uint256 sValue = (amount * ratio) / 1e18;
157:         uint256 sMinted = sValue > 0
158:             ? ISafEth(SAF_ETH_ADDRESS).stake{value: sValue}(0)
159:             : 0;
160:         uint256 vValue = (amount * (1e18 - ratio)) / 1e18;
161:         uint256 vMinted = vValue > 0 ? vStrategy.deposit{value: vValue}() : 0;
162:         totalValue +=
163:             (sMinted * ISafEth(SAF_ETH_ADDRESS).approxPrice(true)) +
164:             (vMinted * vStrategy.price());
165:         if (totalValue == 0) revert FailedToDeposit();
166:         uint256 amountToMint = totalValue / priceBeforeDeposit;
167:         if (amountToMint < _minout) revert BelowMinOut();
168:         _mint(msg.sender, amountToMint);
169:     }
```

Because of rounding and limited precision integer math, the split could potentially not cover the entirety of the deposit, i.e. `sValue + vValue != amount`. There could be some leftovers from both calculations that won't be considered in the deposit.

## Recommendation

Instead of calculating `vValue` using `1e18 - ratio`, simply use difference between the deposited amount and the calculated SafEth amount (`sValue`).

```solidity
uint256 vValue = amount - sValue;
```
