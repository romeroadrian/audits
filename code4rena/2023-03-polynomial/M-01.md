# Collateral may fall short to cover debt being repaid during a liquidation

## Impact

During a liquidation, the system calculates the amount of collateral that should be sent to the liquidator based on the amount of debt being repaid. This is calculated in the `liquidate` function present in the `ShortCollateral` contract:

https://github.com/code-423n4/2023-03-polynomial/blob/main/src/ShortCollateral.sol#L121-L144

```solidity
function liquidate(uint256 positionId, uint256 debt, address user)
    external
    override
    onlyExchange
    nonReentrant
    returns (uint256 totalCollateralReturned)
{
    UserCollateral storage userCollateral = userCollaterals[positionId];

    bytes32 currencyKey = synthetixAdapter.getCurrencyKey(userCollateral.collateral);
    Collateral memory coll = collaterals[currencyKey];

    (uint256 markPrice,) = exchange.getMarkPrice();
    (uint256 collateralPrice,) = synthetixAdapter.getAssetPrice(currencyKey);
    uint256 collateralClaim = debt.mulDivDown(markPrice, collateralPrice);
    uint256 liqBonus = collateralClaim.mulWadDown(coll.liqBonus);
    totalCollateralReturned = liqBonus + collateralClaim;
    if (totalCollateralReturned > userCollateral.amount) totalCollateralReturned = userCollateral.amount;
    userCollateral.amount -= totalCollateralReturned;

    ERC20(userCollateral.collateral).safeTransfer(user, totalCollateralReturned);

    emit LiquidateCollateral(positionId, user, userCollateral.collateral, debt, totalCollateralReturned);
}
```

As we can see, the calculated collateral amount (`totalCollateralReturned` variable) may be greater than the available collateral amount in the position, in which case it will be capped to that value. This is expected, as volatility or extreme market conditions may render the collateral insufficient to cover the debt. However, the liquidation process in the Exchange contract doesn't provide any kind of safeguard for the user to cover against these scenarios in which the expected collateral the liquidator will get differs from the moment the transaction is sent until the transaction is effectively processed by the blockchain.

https://github.com/code-423n4/2023-03-polynomial/blob/main/src/Exchange.sol#L333-L353

```solidity
function _liquidate(uint256 positionId, uint256 debtRepaying) internal {
    uint256 maxDebtRepayment = shortCollateral.maxLiquidatableDebt(positionId);
    require(maxDebtRepayment > 0);
    if (debtRepaying > maxDebtRepayment) debtRepaying = maxDebtRepayment;

    IShortToken.ShortPosition memory position = shortToken.shortPositions(positionId);

    uint256 totalCollateralReturned = shortCollateral.liquidate(positionId, debtRepaying, msg.sender);

    address user = shortToken.ownerOf(positionId);

    uint256 finalPosition = position.shortAmount - debtRepaying;
    uint256 finalCollateralAmount = position.collateralAmount - totalCollateralReturned;

    shortToken.adjustPosition(positionId, user, position.collateral, finalPosition, finalCollateralAmount);

    pool.liquidate(debtRepaying);
    powerPerp.burn(msg.sender, debtRepaying);

    emit Liquidate(user, positionId, msg.sender, debtRepaying, position.collateral, totalCollateralReturned);
}
```

## Proof of Concept

As an easy example, let's assume an unstable market and a position that can be liquidated. 

1. Liquidator sends transaction to call `Exchange.liquidate`.
2. Collateral price tanks and falls short to cover debt.
3. Transaction is executed, liquidator gets less than expected. 

## Recommendation

Add a `minCollateralReturned` parameter to the `liquidate` function of the Exchange contract. Revert the transaction if the calculated collateral to be returned is below this minimum.

```solidity
+ function liquidate(uint256 positionId, uint256 debtRepaying, uint256 minCollateralReturned)
      external
      override
      nonReentrant
      whenNotPaused("EXCHANGE_LIQUIDATE")
  {
+     _liquidate(positionId, debtRepaying, minCollateralReturned);
      _updateFundingRate();
  }

+ function _liquidate(uint256 positionId, uint256 debtRepaying, uint256 minCollateralReturned) internal {
      uint256 maxDebtRepayment = shortCollateral.maxLiquidatableDebt(positionId);
      require(maxDebtRepayment > 0);
      if (debtRepaying > maxDebtRepayment) debtRepaying = maxDebtRepayment;

      IShortToken.ShortPosition memory position = shortToken.shortPositions(positionId);

      uint256 totalCollateralReturned = shortCollateral.liquidate(positionId, debtRepaying, msg.sender);
+     require(totalCollateralReturned >= minCollateralReturned);

      address user = shortToken.ownerOf(positionId);

      uint256 finalPosition = position.shortAmount - debtRepaying;
      uint256 finalCollateralAmount = position.collateralAmount - totalCollateralReturned;

      shortToken.adjustPosition(positionId, user, position.collateral, finalPosition, finalCollateralAmount);

      pool.liquidate(debtRepaying);
      powerPerp.burn(msg.sender, debtRepaying);

      emit Liquidate(user, positionId, msg.sender, debtRepaying, position.collateral, totalCollateralReturned);
  }
```
