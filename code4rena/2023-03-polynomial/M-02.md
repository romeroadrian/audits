# Cached token price may lead to incorrect mint amount while processing queued deposits in batch

## Impact

The LiquidityPool contract supports queueing deposits, which can later be processed in batch using the `processDeposits` function:

https://github.com/code-423n4/2023-03-polynomial/blob/main/src/LiquidityPool.sol#L219-L242

```solidity
219   function processDeposits(uint256 count) external override nonReentrant whenNotPaused("POOL_PROCESS_DEPOSITS") {
220       assert(queuedDepositHead + count - 1 < nextQueuedDepositId);
221       uint256 tokenPrice = getTokenPrice();
222   
223       for (uint256 i = 0; i < count; i++) {
224           QueuedDeposit storage current = depositQueue[queuedDepositHead];
225   
226           if (current.requestedTime == 0 || block.timestamp < current.requestedTime + minDepositDelay) {
227               return;
228           }
229   
230           uint256 tokensToMint = current.depositedAmount.divWadDown(tokenPrice);
231   
232           current.mintedTokens = tokensToMint;
233           totalQueuedDeposits -= current.depositedAmount;
234           totalFunds += current.depositedAmount;
235           liquidityToken.mint(current.user, tokensToMint);
236   
237           emit ProcessDeposit(current.id, current.user, current.depositedAmount, tokensToMint, current.requestedTime);
238   
239           current.depositedAmount = 0;
240           queuedDepositHead++;
241       }
242   }
```

As we can see in the previous snippet, the function calculates the token price in line 221 and uses that cached value to calculate the amount of tokens to mint for each queued deposit being processed (line 230). However, each deposit that is processed modifies the `totalFunds` variable (line 234) and the liquidity token total supply, as new tokens are minted in line 235. These two values are involved in the token price calculation:

https://github.com/code-423n4/2023-03-polynomial/blob/main/src/LiquidityPool.sol#L340-L365

```solidity
function getTokenPrice() public view override returns (uint256) {
    if (totalFunds == 0) {
        return 1e18;
    }

    uint256 totalSupply = liquidityToken.totalSupply() + totalQueuedWithdrawals;
    int256 skew = _getSkew();

    if (skew == 0) {
        return totalFunds.divWadDown(totalSupply);
    }

    (uint256 markPrice, bool isInvalid) = getMarkPrice();
    require(!isInvalid);

    uint256 totalValue = totalFunds;

    uint256 amountOwed = markPrice.mulWadDown(powerPerp.totalSupply());
    uint256 amountToCollect = markPrice.mulWadDown(shortToken.totalShorts());
    uint256 totalMargin = _getTotalMargin();

    totalValue += totalMargin + amountToCollect;
    totalValue -= uint256((int256(amountOwed) + usedFunds));

    return totalValue.divWadDown(totalSupply);
}
```

This means that, if `count > 1` (i.e. `processDeposits` will process more than 1 deposit), each deposit that is processed could potentially affect token price calculation (and liquidity token minting amount) for the next deposits in queue. Caching the token price outside of the loop may yield incorrect token minting amounts for deposits after the first one.

## Recommendation

Move the token price calculation inside the for loop, so it is calculated again in each iteration.

```solidity
function processDeposits(uint256 count) external override nonReentrant whenNotPaused("POOL_PROCESS_DEPOSITS") {
    assert(queuedDepositHead + count - 1 < nextQueuedDepositId);

    for (uint256 i = 0; i < count; i++) {
        uint256 tokenPrice = getTokenPrice();
        
        QueuedDeposit storage current = depositQueue[queuedDepositHead];

        if (current.requestedTime == 0 || block.timestamp < current.requestedTime + minDepositDelay) {
            return;
        }

        uint256 tokensToMint = current.depositedAmount.divWadDown(tokenPrice);

        current.mintedTokens = tokensToMint;
        totalQueuedDeposits -= current.depositedAmount;
        totalFunds += current.depositedAmount;
        liquidityToken.mint(current.user, tokensToMint);

        emit ProcessDeposit(current.id, current.user, current.depositedAmount, tokensToMint, current.requestedTime);

        current.depositedAmount = 0;
        queuedDepositHead++;
    }
}
```
