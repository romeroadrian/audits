# Collateral config parameters can be changed by re-approving the collateral

## Impact

Collaterals are expected to be approved and configured by using the `approveCollateral` function present in the `ShortCollateral` contract. 

https://github.com/code-423n4/2023-03-polynomial/blob/main/src/ShortCollateral.sol#L251-L268

```solidity
function approveCollateral(bytes32 key, uint256 collateralRatio, uint256 liqRatio, uint256 liqBonus)
    external
    requiresAuth
{
    Collateral storage collateral = collaterals[key];
    address synth = synthetixAdapter.getSynth(key);
    require(synth != address(0x0));
    require(liqRatio < collateralRatio);

    collateral.currencyKey = key;
    collateral.synth = synth;
    collateral.isApproved = true;
    collateral.collateralRatio = collateralRatio;
    collateral.liqRatio = liqRatio;
    collateral.liqBonus = liqBonus;

    emit ApproveCollateral(key, collateralRatio, liqRatio, liqBonus);
}
```

However, once a collateral has been approved and configured the function can be called again and used to change (overwrite) the config parameters, as it lacks any verification that it was previously configured. Although this is a permissioned action, it represents a significant risk for existing short positions because these parameters play a direct role in the health of the current debt of those positions. 

## Proof of Concept

In the following test, the `approveCollateral` function is called a second time to override the config parameters for the "sETH" collateral.

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/cb76cba34e1e7f5ec5b223b91c3d86a3).

```solidity
function test_ShortCollateral_approveCollateral_ChangeConfigParams() public {
    bytes32 assetKey = "sETH";
    uint256 collRatio = 1.5e18;
    uint256 liqRatio = 1.3e18;
    uint256 liqBonus = 1e17;

    shortCollateral.approveCollateral(assetKey, collRatio, liqRatio, liqBonus);

    (bytes32 key, address synth, bool isApproved, uint256 collRatio_, uint256 liqRatio_, uint256 liqBonus_) = shortCollateral.collaterals(assetKey);

    assertTrue(isApproved);

    // approveCollateral can be called again to change collateral config parameters

    uint256 newCollRatio = 1.2e18;
    uint256 newLiqRatio = 1.1e18;
    uint256 newLiqBonus = 1e16;

    shortCollateral.approveCollateral(assetKey, newCollRatio, newLiqRatio, newLiqBonus);

    (key, synth, isApproved, collRatio_, liqRatio_, liqBonus_) = shortCollateral.collaterals(assetKey);

    assertEq(newCollRatio, collRatio_);
    assertEq(newLiqRatio, liqRatio_);
    assertEq(newLiqBonus, liqBonus_);
}
```

## Recommendation

Check if the collateral has been previously enabled to prevent a re-initialization of the collateral config.

```solidity
  function approveCollateral(bytes32 key, uint256 collateralRatio, uint256 liqRatio, uint256 liqBonus)
      external
      requiresAuth
  {
      Collateral storage collateral = collaterals[key];
      address synth = synthetixAdapter.getSynth(key);
      require(synth != address(0x0));
      require(liqRatio < collateralRatio);
+     require(!collateral.isApproved);

      collateral.currencyKey = key;
      collateral.synth = synth;
      collateral.isApproved = true;
      collateral.collateralRatio = collateralRatio;
      collateral.liqRatio = liqRatio;
      collateral.liqBonus = liqBonus;

      emit ApproveCollateral(key, collateralRatio, liqRatio, liqBonus);
  }
```
