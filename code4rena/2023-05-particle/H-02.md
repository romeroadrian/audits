# Borrowers can still close loan normally while being defaulted

A borrower can repay a loan normally while having outstanding debt and close it causing losses to the lender.

## Impact

Loans in the Particle protocol are subject to an interest rate defined by the lender. A borrower deposits a certain margin while taking a loan which is stored in the `credit` field of the lien. Over time interests are accrued to the lender which are then paid from the borrower's credit.

If a loan gets defaulted, i.e. the credit is not enough to account for the payable interests, the lender has the ability to call the `withdrawEthWithInterest()` function in order to liquidate a borrower. 

https://github.com/code-423n4/2023-05-particle/blob/main/contracts/protocol/ParticleExchange.sol#L192-L223

```solidity
192:     function withdrawEthWithInterest(Lien calldata lien, uint256 lienId) external override validateLien(lien, lienId) {
193:         if (msg.sender != lien.lender) {
194:             revert Errors.Unauthorized();
195:         }
196: 
197:         if (lien.loanStartTime == 0) {
198:             revert Errors.InactiveLoan();
199:         }
200: 
201:         uint256 payableInterest = _calculateCurrentPayableInterest(lien);
202: 
203:         // verify that the liquidation condition has met (borrower insolvent or auction concluded)
204:         if (payableInterest < lien.credit && !_auctionConcluded(lien.auctionStartTime)) {
205:             revert Errors.LiquidationHasNotReached();
206:         }
207: 
208:         // delete lien (delete first to prevent reentrancy)
209:         delete liens[lienId];
210: 
211:         // transfer ETH with interest back to lender
212:         payable(lien.lender).transfer(lien.price + payableInterest);
213: 
214:         // transfer PnL to borrower
215:         if (lien.credit > payableInterest) {
216:             payable(lien.borrower).transfer(lien.credit - payableInterest);
217:         }
218: 
219:         emit WithdrawETH(lienId);
220: 
221:         // withdraw interest from this account too
222:         _withdrawAccountInterest(payable(msg.sender));
223:     }
```

Line 204 checks that payable interests are at least equal to the lien's credit. If conditions are met, then line 212 transfers the lender the lien's price along with the payable interests. Notice that the implementation of the `_calculateCurrentPayableInterest` function caps the payable interest to the `credit` fields of the lien. This is needed as the lender can't really receive more ETH than the available credit portion.

https://github.com/code-423n4/2023-05-particle/blob/main/contracts/protocol/ParticleExchange.sol#L754-L758

```solidity
754:     function _calculateCurrentPayableInterest(Lien calldata lien) internal view returns (uint256) {
755:         uint256 payableInterest = MathUtils.calculateCurrentInterest(lien.price, lien.rate, lien.loanStartTime);
756:         payableInterest = payableInterest > lien.credit ? lien.credit : payableInterest;
757:         return payableInterest;
758:     }
```

However, it is important to note that a borrower can close a loan normally by calling `buyNftFromMarket()`, `repayWithNft()` or `refinanceLoan()` **even if their loan is defaulted**, there's nothing guarding against this possibility. At first this may seem desirable, the borrower is actually closing the loan and repaying the lender. But there is one particular detail that may allow the borrower to not repay their loan in full. All 3 cases are similar, but let's focus on the implementation of `repayWithNft()`:

https://github.com/code-423n4/2023-05-particle/blob/main/contracts/protocol/ParticleExchange.sol#L466-L515

```solidity
466:     function repayWithNft(
467:         Lien calldata lien,
468:         uint256 lienId,
469:         uint256 tokenId
470:     ) external override validateLien(lien, lienId) {
471:         if (msg.sender != lien.borrower) {
472:             revert Errors.Unauthorized();
473:         }
474: 
475:         if (lien.loanStartTime == 0) {
476:             revert Errors.InactiveLoan();
477:         }
478: 
479:         // update lien (by default, the lien is open to accept new loan)
480:         /// @dev update lien before paybacks to prevent reentrancy
481:         liens[lienId] = keccak256(
482:             abi.encode(
483:                 Lien({
484:                     lender: lien.lender,
485:                     borrower: address(0),
486:                     collection: lien.collection,
487:                     tokenId: tokenId,
488:                     price: lien.price,
489:                     rate: lien.rate,
490:                     loanStartTime: 0,
491:                     credit: 0,
492:                     auctionStartTime: 0
493:                 })
494:             )
495:         );
496: 
497:         // transfer NFT to the contract
498:         /// @dev collection.setApprovalForAll should have been called by this point
499:         IERC721(lien.collection).safeTransferFrom(msg.sender, address(this), tokenId);
500: 
501:         // accure interest to lender
502:         uint256 payableInterest = _calculateCurrentPayableInterest(lien);
503:         interestAccrued[lien.lender] += payableInterest;
504: 
505:         // pay PnL to borrower
506:         // since: credit = previous sold amount + position margin - lien.price
507:         // and:   payback = previous sold amount + position margin - interest
508:         // hence: payback = credit + lien.price - interest
509:         uint256 payback = lien.credit + lien.price - payableInterest;
510:         if (payback > 0) {
511:             payable(lien.borrower).transfer(payback);
512:         }
513: 
514:         emit RepayWithNFT(lienId, tokenId);
515:     }
```

As we can see in the previous snippet of code, the borrower transfers the NFT back in line 499 and interests are accrued to the lender in line 502. However, we need to recall that the calculation of `_calculateCurrentPayableInterest()` is capped to the available credit. This means that `payableInterest` will be at most `lien.credit` and the calculation in line 509 won't overflow. The function will succeed even if the borrower hasn't repaid their debt in full.

This means that a borrower can effectively close a loan without repaying the debt, causing losses to the lender.

Note also that a borrower may intentionally take advantage of this situation and eventually front-run the lender when they try to call `withdrawEthWithInterest()` to liquidate the borrower.

## Proof of concept

1. Alice creates a lien with a price of 5 ETH and a rate of 100%.
2. Bob takes the loan and supplies 5 ETH to cover the lien price and 0.1 ETH to cover for potential interests.
3. 10 days later, the payable interest is ~0.137 ETH.
4. Bob can repay the loan normally by supplying the NFT back. Alice will only get 0.1 ETH as the interest of the loan.

## Recommendation

The borrower should not be allowed to close the loan while having outstanding debt, i.e. if the payable interests are greater than the current credit. In this situation, the borrower should first add margin by calling `addCredit()` to cover for the missing debt and then close the loan.
