# Party members using smart wallets cannot use the OffChainSignatureValidator

## Summary

The OffChainSignatureValidator relies on a signature created by an EOA using a private key, which won't work for smart wallets that are implemented using contracts.

## Impact

One of the introduced changes is the ability for a Party to sign messages using ERC-1271. The base implementation can be found in the ProposalExecutionEngine contract:

https://github.com/code-423n4/2023-10-party/blob/0ce3819de173f7688c9c834ce2cc758dd03c9bd2/contracts/proposals/ProposalExecutionEngine.sol#L225-L245

```solidity
225:     function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4) {
226:         IERC1271 validator = getSignatureValidatorForHash(hash);
227:         if (address(validator) == address(1)) {
228:             // Signature set by party to be always valid
229:             return IERC1271.isValidSignature.selector;
230:         }
231:         if (address(validator) != address(0)) {
232:             return validator.isValidSignature(hash, signature);
233:         }
234:         if (tx.origin == address(0)) {
235:             validator = getSignatureValidatorForHash(0);
236:             if (address(validator) == address(0)) {
237:                 // Use global off-chain signature validator
238:                 validator = IERC1271(
239:                     _GLOBALS.getAddress(LibGlobals.GLOBAL_OFF_CHAIN_SIGNATURE_VALIDATOR)
240:                 );
241:             }
242:             return validator.isValidSignature(hash, signature);
243:         }
244:         return 0;
245:     }
```

As we can see in the previous snippet of code, the default behavior for off-chain actions (i.e. when `tx.origin == address(0)`) will delegate the request to the OffChainSignatureValidator.

The OffChainSignatureValidator contract validates hashes by using signatures coming from an EOA. The implementation recovers the signer (Party member) using `ecrecover()` from the given set of `r`, `s` and `v` values. This leaves out of the picture all _smart wallets_ or members being represented by smart contracts, such as Gnosis Safe, account abstraction implementations, or timelocked governance controllers.

Party members using these types of contracts won't be able to validate signatures using the OffChainSignatureValidator.

## Proof of Concept

As we can see in the following snippet of code, the implementation of `isValidSignature()` requires a signature coming from an EOA's private key. The signer is obtained using `ecrecover()`.

https://github.com/code-423n4/2023-10-party/blob/0ce3819de173f7688c9c834ce2cc758dd03c9bd2/contracts/signature-validators/OffChainSignatureValidator.sol#L28-L58

```solidity
28:     function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4) {
29:         uint8 v;
30:         bytes32 r;
31:         bytes32 s;
32:         assembly {
33:             // First word of signature after size contains r
34:             r := mload(add(signature, 0x20))
35:             s := mload(add(signature, 0x40))
36:             // v is one byte which starts after s. type is uint8 so extra data will be ignored
37:             v := mload(add(signature, 0x41))
38:         }
39: 
40:         bytes memory message;
41:         assembly {
42:             // Raw message data begins after v. Overwriting part of s and v with size of `message`
43:             message := add(signature, 0x41)
44:             mstore(message, sub(mload(signature), 0x41))
45:         }
46: 
47:         // Recreate the message pre-hash from the raw data
48:         bytes memory encodedPacket = abi.encodePacked(
49:             "\x19Ethereum Signed Message:\n",
50:             Strings.toString(message.length),
51:             message
52:         );
53:         if (keccak256(encodedPacket) != hash) {
54:             revert MessageHashMismatch();
55:         }
56: 
57:         Party party = Party(payable(msg.sender));
58:         address signer = ecrecover(hash, v, r, s);
```

## Recommendation

Similar to how Parties use ERC-1271, the implementation of OffChainSignatureValidator could also add the option to use this standard. The signature type (Ethreum Signed Message for an EOA or EIP-1271) could be encoded into the signature data payload, and the implementation can switch on this type to provide both alternatives. This could also help being future proof in case more signing types need to be added.
