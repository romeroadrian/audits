# OffChainSignatureValidator doesn't validate length of signature payload

## Summary

The implementation of `isValidSignature()` in OffChainSignatureValidator uses low level assembly to read the signature payload without validating its length.

## Impact

The implementation of `isValidSignature()` uses low level assembly to parse and extract the required values needed to validate the signature:

https://github.com/code-423n4/2023-10-party/blob/0ce3819de173f7688c9c834ce2cc758dd03c9bd2/contracts/signature-validators/OffChainSignatureValidator.sol#L28-L55

```solidity
28:     function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4) {
29:         uint8 v;
30:         bytes32 r;
31:         bytes32 s;
32:         assembly {
33:             // First word of signature after size contains r
34:             r := mload(add(signature, 0x20))
35:             s := mload(add(signature, 0x40))
36:             // v is one byte which starts after s. type is uint8 so extra data will be ignored
37:             v := mload(add(signature, 0x41))
38:         }
39: 
40:         bytes memory message;
41:         assembly {
42:             // Raw message data begins after v. Overwriting part of s and v with size of `message`
43:             message := add(signature, 0x41)
44:             mstore(message, sub(mload(signature), 0x41))
45:         }
46: 
47:         // Recreate the message pre-hash from the raw data
48:         bytes memory encodedPacket = abi.encodePacked(
49:             "\x19Ethereum Signed Message:\n",
50:             Strings.toString(message.length),
51:             message
52:         );
53:         if (keccak256(encodedPacket) != hash) {
54:             revert MessageHashMismatch();
55:         }
```

The intended encoding of the `signature` argument is the following:

```
    /---------------------------------------------------------------------------------------------------------------------\
    | total length (32 bytes) |   r parameter (32 bytes)   |   s parameter (32 bytes)   | v (1 byte) | inner data ....... |
    \---------------------------------------------------------------------------------------------------------------------/

    ^
    |
 signature
  pointer
```

The function first reads `signature + 0x20` to read `r`, `signature + 0x40` to read `s` and `signature + 0x41` to read `v`. Then it copies the total length minus 65 into position `0x41` to place the length of the inner data just before the inner data begins, in order to then represent the inner data payload by re-using the `signature` variable.

The low level handling is correct, however, the implementation misses to first validate the size of the `signatures` argument. When the size of the payload is shorter than expected, the function will read memory outside of the intended region. Not only that, it will also write memory outside the intended area, potentially overwriting values from the surrounding region.

## Proof of Concept

As an example, when the `signature` payload is empty, Solidity will represent this dynamic array using [the zero slot (`0x60`)](https://docs.soliditylang.org/en/v0.8.23/internals/layout_in_memory.html#layout-in-memory). This means that the implementation will start reading invalid memory (`r` will be `0x80`, `s` will be `0xA0`, etc), and will also write try to write at position `0xA1` (`0x60 + 0x41 = 0xA1`).

## Recommendation

Validate that `signature.length` is at least 65 bytes long. This number considers:

- Signature argument `r` (32 bytes)
- Signature argument `s` (32 bytes)
- Signature argument `v` (1 byte)

```diff
    function isValidSignature(bytes32 hash, bytes memory signature) external view returns (bytes4) {
+       require(signature.length >= 65);
        uint8 v;
        bytes32 r;
        bytes32 s;
```
