# Wallet fallback does not fail when there is no handler

The wallet fallback function will silently succeed when there is no fallback handler, allowing attacks or accidents due to the introduction of phantom functions.

## Impact

The Ambire wallet functionality can be extended by defining a fallback handler. Received calls to functions not present in the contract are delegated to this handler using the `fallback()` function:

https://github.com/AmbireTech/ambire-common/blob/5c54f8005e90ad481df8e34e85718f3d2bfa2ace/contracts/AmbireAccount.sol#L92-L95

```solidity
92: 	fallback() external payable {
93: 		// We store the fallback handler at this magic slot
94: 		address fallbackHandler = address(uint160(uint(privileges[FALLBACK_HANDLER_SLOT])));
95: 		if (fallbackHandler == address(0)) return;
```

As we can in the previous snippet of code, line 95 deals with an undefined handler by simply returning from the function instead of reverting. This effectively opens the doors to _phantom functions_: calling any unimplemented function will succeed from the point of view of the caller. 

The issue can result in different types of impact, accidental or intentional:

1. Integrations around the wallet that call an unimplemented function can potentially assume the call succeeded with certain expected effect.
2. Attackers can take advantage of calls to the wallet that don't revert, and intentionally abuse of certain integrations that interpret a non-reverting call as a successful call. As an example, [this article](https://media.dedaub.com/phantom-functions-and-the-billion-dollar-no-op-c56f062ae49f) describes a critical issue that relies on phantom functions.

## Proof of Concept

The following test illustrates the issue. The account doesn't have any fallback handler and will succeed on any call to any unimplemented function that doesn't expect return data.

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/535a969c96e0a6f78781287bd0931b6a).

```solidity
interface ACoolWalletFeature {
    function doSomethingGreatInWallet(uint256) external;
}

function test_AmbireAccount_FallbackDoesntRevert() public {
    address user = makeAddr("user");

    address[] memory addrs = new address[](1);
    addrs[0] = user;
    AmbireAccount account = new AmbireAccount(addrs);

    // wallet doesn't have fallback
    assertEq(account.privileges(address(0x6969)), bytes32(0));

    // the following doesnt fail
    ACoolWalletFeature(address(account)).doSomethingGreatInWallet(42);
}
```

## Recommendation

Revert if the fallback handler is not defined.

```solidity
  fallback() external payable {
    // We store the fallback handler at this magic slot
    address fallbackHandler = address(uint160(uint(privileges[FALLBACK_HANDLER_SLOT])));
@   if (fallbackHandler == address(0)) revert();
    assembly {
      // We can use memory addr 0, since it's not occupied
      calldatacopy(0, 0, calldatasize())
      let result := delegatecall(gas(), fallbackHandler, 0, calldatasize(), 0, 0)
      let size := returndatasize()
      returndatacopy(0, 0, size)
      if eq(result, 0) {
        revert(0, size)
      }
      return(0, size)
    }
  }
```
