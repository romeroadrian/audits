# Factory can potentially brick accounts that deploy a initcode that returns empty

The AmbireAccountFactory contract does not check that the deployed account actually has code leading to different undesired outcomes.

## Impact

The factory implementation for the Ambire wallet deploys new instances using CREATE2 to create new contracts based on an arbitrary code taken as parameter. The core of the logic lies in the `deploySafe()` function:

https://github.com/AmbireTech/ambire-common/blob/5c54f8005e90ad481df8e34e85718f3d2bfa2ace/contracts/AmbireAccountFactory.sol#L44-L64

```solidity
44: 	function deploySafe(bytes memory code, uint256 salt) internal returns (address) {
45: 		address expectedAddr = address(
46: 			uint160(uint256(keccak256(abi.encodePacked(bytes1(0xff), address(this), salt, keccak256(code)))))
47: 		);
48: 		uint256 size;
49: 		assembly {
50: 			size := extcodesize(expectedAddr)
51: 		}
52: 		// If there is code at that address, we can assume it's the one we were about to deploy,
53: 		// because of how CREATE2 and keccak256 works
54: 		if (size == 0) {
55: 			address addr;
56: 			assembly {
57: 				addr := create2(0, add(code, 0x20), mload(code), salt)
58: 			}
59: 			require(addr != address(0), 'FAILED_DEPLOYING');
60: 			require(addr == expectedAddr, 'FAILED_MATCH');
61: 			emit LogDeployed(addr, salt);
62: 		}
63: 		return expectedAddr;
64: 	}
```

As we can see in the previous snippet code, the implementation validates that the account was created (`addr != address(0)`) and also ensures the address is in fact the expected one (`addr == expectedAddr`). However, it fails to actually validate that the account has code (a condition that is later used in line 54 to check if the wallet has been already deployed). Contracts can be successfully created (i.e. CREATE or CREATE2 returns a valid address) while also having an empty code. This can happen if the initcode returns an empty result or if execution reaches a normal STOP opcode.

This situation is warned in the [Yellow Paper](https://ethereum.github.io/yellowpaper/paper.pdf) section 7.1, which refers to these cases as _zombie accounts_:

> For a normal STOP code, or if the code returned is otherwise empty, then the state is left with a zombie account, and any remaining balance will be locked into the account forever.

This also breaks the [EIP-4337](https://eips.ethereum.org/EIPS/eip-4337) requirement that the initcode must deploy a contract:

> Create the account if it does not yet exist, using the initcode provided in the UserOperation. If the account does not exist, and the initcode is empty, or does not deploy a contract at the “sender” address, the call must fail.

Even though the Ambire wallet doesn't implement EIP-4337 yet, this may conflict with a future planned support (see [README](https://github.com/code-423n4/2023-05-ambire/blob/main/README.md#known-tradeoffs)).

## Proof of Concept

Here we demonstrate the issue by deploying a code that executes a "STOP" (`00` in bytecode) which effectively leads to deploying a zombie account.

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/535a969c96e0a6f78781287bd0931b6a).

```solidity
function test_AmbireAccountFactory_EmptyBytecode() public {
    AmbireAccountFactory factory = new AmbireAccountFactory(address(0));

    bytes memory code = hex"00";
    uint256 salt = 42;

    address expectedAddr = address(
      uint160(uint256(keccak256(abi.encodePacked(bytes1(0xff), address(factory), salt, keccak256(code)))))
    );

    factory.deploy(code, salt);

    // deploy succeeded but account has no code
    assertEq(expectedAddr.code.length, 0);

    // this means that a new call to deploy will fail, as the account already exists
    // but deploySafe will think otherwise because codesize is zero
    vm.expectRevert();
    factory.deploy(code, salt);
}
```

## Recommendation

Add a check to validate that the created account has code.

```solidity
  function deploySafe(bytes memory code, uint256 salt) internal returns (address) {
    address expectedAddr = address(
      uint160(uint256(keccak256(abi.encodePacked(bytes1(0xff), address(this), salt, keccak256(code)))))
    );
    uint256 size;
    assembly {
      size := extcodesize(expectedAddr)
    }
    // If there is code at that address, we can assume it's the one we were about to deploy,
    // because of how CREATE2 and keccak256 works
    if (size == 0) {
      address addr;
      assembly {
        addr := create2(0, add(code, 0x20), mload(code), salt)
      }
      require(addr != address(0), 'FAILED_DEPLOYING');
+     require(addr.code.length > 0, 'FAILED_DEPLOYING');
      require(addr == expectedAddr, 'FAILED_MATCH');
      emit LogDeployed(addr, salt);
    }
    return expectedAddr;
  }
```
