# Low level calls to accounts with no code succeed in AmbireAccount

Calls in the AmbireAccount contract may suffer from unnoticed unexpected behavior due to how they are executed.

## Impact

Transactions in the AmbireAccount contract are executed by doing low level calls. This is present in the implementations of `tryCatch()`, `tryCatchLimit()` and `executeCall()`:

https://github.com/AmbireTech/ambire-common/blob/5c54f8005e90ad481df8e34e85718f3d2bfa2ace/contracts/AmbireAccount.sol#L119-L123

```solidity
119: 	function tryCatch(address to, uint256 value, bytes calldata data) external payable {
120: 		require(msg.sender == address(this), 'ONLY_IDENTITY_CAN_CALL');
121: 		(bool success, bytes memory returnData) = to.call{ value: value, gas: gasleft() }(data);
122: 		if (!success) emit LogErr(to, value, data, returnData);
123: 	}
```

https://github.com/AmbireTech/ambire-common/blob/5c54f8005e90ad481df8e34e85718f3d2bfa2ace/contracts/AmbireAccount.sol#L126-L130

```solidity
126: 	function tryCatchLimit(address to, uint256 value, bytes calldata data, uint256 gasLimit) external payable {
127: 		require(msg.sender == address(this), 'ONLY_IDENTITY_CAN_CALL');
128: 		(bool success, bytes memory returnData) = to.call{ value: value, gas: gasLimit }(data);
129: 		if (!success) emit LogErr(to, value, data, returnData);
130: 	}
```

https://github.com/AmbireTech/ambire-common/blob/5c54f8005e90ad481df8e34e85718f3d2bfa2ace/contracts/AmbireAccount.sol#L226-L237

```solidity
226: 	function executeCall(address to, uint256 value, bytes memory data) internal {
227: 		assembly {
228: 			let result := call(gas(), to, value, add(data, 0x20), mload(data), 0, 0)
229: 
230: 			if eq(result, 0) {
231: 				let size := returndatasize()
232: 				let ptr := mload(0x40)
233: 				returndatacopy(ptr, 0, size)
234: 				revert(ptr, size)
235: 			}
236: 		}
237: 	}
```

In all of these cases a low level call is used to execute the external call to the given target address with the corresponding payload to be used as calldata.

Low level calls to accounts with no code will succeed. Quoting the reference for the CALL opcode in [evm.codes](https://www.evm.codes/#f1):

> Creates a new sub context and execute the code of the given account, then resumes the current one. Note that an account with no code will return success as true.

This means that an action (that potentially has calldata, to discriminate the case of simple transfer of value) to an account with no code will succeed without any errors. These cases will be interpreted as successfully executed, which may result in an unexpected behavior that might go unnoticed.

## Proof of Concept

The following test illustrates the issue. We simulate a transaction which is accidentally sent to an account with no implementation (`address(0x42)`). Even though the target contract doesn't exist, the transaction is successfully executed.

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/535a969c96e0a6f78781287bd0931b6a).

```solidity 
function test_AmbireAccount_LowLevelCallsToAccountsWithNoCode() public {
    address user = makeAddr("user");

    address[] memory addrs = new address[](1);
    addrs[0] = user;
    AmbireAccount account = new AmbireAccount(addrs);

    AmbireAccount.Transaction[] memory txns = new AmbireAccount.Transaction[](1);
    txns[0].to = address(0x42);
    txns[0].value = 0;
    txns[0].data = abi.encodeWithSignature("someFunction(address,uint256)", user, 42);

    // destination doesnt have any code
    assertEq(txns[0].to.code.length, 0);

    // transaction succeeds
    vm.prank(user);
    account.executeBySender(txns);
}
```

## Recommendation

The `tryCatch()` and `tryCatchLimit()` functions should validate that the target account has code, as the intention in both of these cases is to execute a function in the `to` account.

In the `executeCall()` function we would need to discriminate the case of a simple transfer of ETH value (i.e. where the target account may indeed be an EOA without any code). Here we can check if the transaction has calldata (i.e. `transaction.data.length > 0`) in order to validate that the `to` account has non-empty code. 

Another alternative would be to use a strategy similar to how `verifyCallResultFromTarget` works in the `Address` library of the OpenZeppelin contracts, in which they check the account's code length if the returndata length is zero. Again, this path should also contemplate the case when calldata is empty to allow transfers of value that are not intended to execute any code.
