# Royalty fees may be incorrectly accounted in the `buy` and `sell` functions of the PrivatePool contract

## Impact

In both `buy` and `sell` functions of the PrivatePool contract there may be royalty fees associated with the operations. These fees are paid by the caller of the function.

In both cases, royalty recipient and amount are fetched from a lookup address. In the case of `buy`, the fees for each token are accumulated in a `royaltyFeeAmount` which is then added to the `netInputAmount`.

https://github.com/code-423n4/2023-04-caviar/blob/main/src/PrivatePool.sol#L238-L252

```solidity
238:         for (uint256 i = 0; i < tokenIds.length; i++) {
239:             // transfer the NFT to the caller
240:             ERC721(nft).safeTransferFrom(address(this), msg.sender, tokenIds[i]);
241: 
242:             if (payRoyalties) {
243:                 // get the royalty fee for the NFT
244:                 (uint256 royaltyFee,) = _getRoyalty(tokenIds[i], salePrice);
245: 
246:                 // add the royalty fee to the total royalty fee amount
247:                 royaltyFeeAmount += royaltyFee;
248:             }
249:         }
250: 
251:         // add the royalty fee amount to the net input aount
252:         netInputAmount += royaltyFeeAmount;
```

The function then transfers these fees to the recipients:

https://github.com/code-423n4/2023-04-caviar/blob/main/src/PrivatePool.sol#L271-L285

```solidity
271:         if (payRoyalties) {
272:             for (uint256 i = 0; i < tokenIds.length; i++) {
273:                 // get the royalty fee for the NFT
274:                 (uint256 royaltyFee, address recipient) = _getRoyalty(tokenIds[i], salePrice);
275: 
276:                 // transfer the royalty fee to the recipient if it's greater than 0
277:                 if (royaltyFee > 0 && recipient != address(0)) {
278:                     if (baseToken != address(0)) {
279:                         ERC20(baseToken).safeTransfer(recipient, royaltyFee);
280:                     } else {
281:                         recipient.safeTransferETH(royaltyFee);
282:                     }
283:                 }
284:             }
285:         }
```

Note that here, fees are only sent if `recipient != address(0)`, a condition that was not present in the first snippet of code. This means that if `royaltyFee > 0` but `recipient == address(0)`, then royalty fees will be accounted for in the `netInputAmount` that the caller must pay, but won't be distributed since the recipient is the zero address.

A similar case happens in the `sell` function. Royalty fees are accounted for in the total amount in line 341, but are only sent if the recipient is not the zero address (line 344). 

https://github.com/code-423n4/2023-04-caviar/blob/main/src/PrivatePool.sol#L328-L355

```solidity
328:         uint256 royaltyFeeAmount = 0;
329:         for (uint256 i = 0; i < tokenIds.length; i++) {
330:             // transfer each nft from the caller
331:             ERC721(nft).safeTransferFrom(msg.sender, address(this), tokenIds[i]);
332: 
333:             if (payRoyalties) {
334:                 // calculate the sale price (assume it's the same for each NFT even if weights differ)
335:                 uint256 salePrice = (netOutputAmount + feeAmount + protocolFeeAmount) / tokenIds.length;
336: 
337:                 // get the royalty fee for the NFT
338:                 (uint256 royaltyFee, address recipient) = _getRoyalty(tokenIds[i], salePrice);
339: 
340:                 // tally the royalty fee amount
341:                 royaltyFeeAmount += royaltyFee;
342: 
343:                 // transfer the royalty fee to the recipient if it's greater than 0
344:                 if (royaltyFee > 0 && recipient != address(0)) {
345:                     if (baseToken != address(0)) {
346:                         ERC20(baseToken).safeTransfer(recipient, royaltyFee);
347:                     } else {
348:                         recipient.safeTransferETH(royaltyFee);
349:                     }
350:                 }
351:             }
352:         }
353: 
354:         // subtract the royalty fee amount from the net output amount
355:         netOutputAmount -= royaltyFeeAmount;
```

## Recommendation

In both cases, the royalty fee should only be accounted for if the royalty recipient is not the zero address.

For the `buy` function:

```solidity
for (uint256 i = 0; i < tokenIds.length; i++) {
    // transfer the NFT to the caller
    ERC721(nft).safeTransferFrom(address(this), msg.sender, tokenIds[i]);

    if (payRoyalties) {
        // get the royalty fee for the NFT
        (uint256 royaltyFee, address recipient) = _getRoyalty(tokenIds[i], salePrice);

        // add the royalty fee to the total royalty fee amount
        if (recipient != address(0)) {
          royaltyFeeAmount += royaltyFee;
        }
    }
}
```

For the `sell` function:

```solidity
if (payRoyalties) {
    // calculate the sale price (assume it's the same for each NFT even if weights differ)
    uint256 salePrice = (netOutputAmount + feeAmount + protocolFeeAmount) / tokenIds.length;

    // get the royalty fee for the NFT
    (uint256 royaltyFee, address recipient) = _getRoyalty(tokenIds[i], salePrice);

    // transfer the royalty fee to the recipient if it's greater than 0
    if (royaltyFee > 0 && recipient != address(0)) {
        // tally the royalty fee amount
        royaltyFeeAmount += royaltyFee;
    
        if (baseToken != address(0)) {
            ERC20(baseToken).safeTransfer(recipient, royaltyFee);
        } else {
            recipient.safeTransferETH(royaltyFee);
        }
    }
}
```
