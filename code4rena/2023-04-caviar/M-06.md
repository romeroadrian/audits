# Incorrect ETH amount sent to `change` function in EthRouter

## Impact

The `change` function present in the EthRouter contract can be used to execute multiple change actions from multiple private pools using a single transaction.

https://github.com/code-423n4/2023-04-caviar/blob/main/src/EthRouter.sol#L254-L293

```solidity
254:     function change(Change[] calldata changes, uint256 deadline) public payable {
255:         // check deadline has not passed (if any)
256:         if (block.timestamp > deadline && deadline != 0) {
257:             revert DeadlinePassed();
258:         }
259: 
260:         // loop through and execute the changes
261:         for (uint256 i = 0; i < changes.length; i++) {
262:             Change memory _change = changes[i];
263: 
264:             // transfer NFTs from caller
265:             for (uint256 j = 0; j < changes[i].inputTokenIds.length; j++) {
266:                 ERC721(_change.nft).safeTransferFrom(msg.sender, address(this), _change.inputTokenIds[j]);
267:             }
268: 
269:             // approve pair to transfer NFTs from router
270:             ERC721(_change.nft).setApprovalForAll(_change.pool, true);
271: 
272:             // execute change
273:             PrivatePool(_change.pool).change{value: msg.value}(
274:                 _change.inputTokenIds,
275:                 _change.inputTokenWeights,
276:                 _change.inputProof,
277:                 _change.stolenNftProofs,
278:                 _change.outputTokenIds,
279:                 _change.outputTokenWeights,
280:                 _change.outputProof
281:             );
282: 
283:             // transfer NFTs to caller
284:             for (uint256 j = 0; j < changes[i].outputTokenIds.length; j++) {
285:                 ERC721(_change.nft).safeTransferFrom(address(this), msg.sender, _change.outputTokenIds[j]);
286:             }
287:         }
288: 
289:         // refund any surplus ETH to the caller
290:         if (address(this).balance > 0) {
291:             msg.sender.safeTransferETH(address(this).balance);
292:         }
293:     }
```

Change operations are subject to change fees, private pools owners can configure a change fee and there may also be protocol fees. To cover these potential costs, the `change` function allows ETH payments that are forwarded to the `change` function in the Private Pool. However, as we can see in line 273, the implementation forwards the whole `msg.value` amount to the Private Pool `change` functions. This means that if any of the individual calls consumes some ETH for fees, then any subsequent call will fail because the implementation will try to send `msg.value` again and the contract will lack that amount as some of it has been already consumed.

For example, if the user is trying to execute two different change operations, and each one takes 0.1 ETH in fees, then the user will call `change` sending 0.2 ETH (see PoC for a detailed walkthrough):

1. User calls EthRouter `change` with 0.2 ETH.
2. EthRouter executes first change and sends 0.2 ETH to the first Private Pool. The pool returns 0.1 ETH. 
3. EthRouter tries to execute the second change operation, it will try to send 0.2 ETH to the second Private Pool. The operation will fail as the current balance in the EthRouter contract is 0.1 ETH.

The issue will prevent users from using this feature to execute multiple change operations when at least one of the actions (different than the last one) contains change fees.

## Proof of Concept

The following test illustrates the described issue. Alice tries to execute two different change operations using two private pools Each operation has a fee of 0.0025 ETH, so she sends 0.005 ETH. Even though everything looks correct, the operation will fail as the EthRouter contract will try to send 0.005 ETH again during the second change operation.

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/06238839330315780b90d9202042ea0f).

```solidity
function test_EthRouter_change_IncorrectCallValue() public {
    // Setup pools
    PrivatePool privatePool1 = new PrivatePool(
        address(factory),
        address(royaltyRegistry),
        address(stolenNftOracle)
    );
    privatePool1.initialize(
        address(0), // address _baseToken,
        address(milady), // address _nft,
        100e18, // uint128 _virtualBaseTokenReserves,
        10e18, // uint128 _virtualNftReserves,
        25, // uint56 _changeFee,
        0, // uint16 _feeRate,
        bytes32(0), // bytes32 _merkleRoot,
        false, // bool _useStolenNftOracle,
        false // bool _payRoyalties
    );
    PrivatePool privatePool2 = new PrivatePool(
        address(factory),
        address(royaltyRegistry),
        address(stolenNftOracle)
    );
    privatePool2.initialize(
        address(0), // address _baseToken,
        address(milady), // address _nft,
        100e18, // uint128 _virtualBaseTokenReserves,
        10e18, // uint128 _virtualNftReserves,
        25, // uint56 _changeFee,
        0, // uint16 _feeRate,
        bytes32(0), // bytes32 _merkleRoot,
        false, // bool _useStolenNftOracle,
        false // bool _payRoyalties
    );
    // pool 1 has milady 1, pool 2 has milady 2
    uint256 tokenId1 = 1;
    uint256 tokenId2 = 2;
    milady.mint(address(privatePool1), tokenId1);
    milady.mint(address(privatePool2), tokenId2);
    
    // Now alice tries to change both tokens from both pools. Alice has milady 3 and 4
    vm.startPrank(alice);
    
    vm.deal(alice, 1 ether);
    
    uint256 tokenId3 = 3;
    uint256 tokenId4 = 4;
    milady.mint(address(alice), tokenId3);
    milady.mint(address(alice), tokenId4);
    
    milady.setApprovalForAll(address(ethRouter), true);
    
    EthRouter.Change[] memory changes = new EthRouter.Change[](2);
    
    changes[0].pool = payable(privatePool1);
    changes[0].nft = address(milady);
    changes[0].inputTokenIds = new uint256[](1);
    changes[0].inputTokenIds[0] = tokenId3;
    changes[0].outputTokenIds = new uint256[](1);
    changes[0].outputTokenIds[0] = tokenId1;
    
    changes[1].pool = payable(privatePool2);
    changes[1].nft = address(milady);
    changes[1].inputTokenIds = new uint256[](1);
    changes[1].inputTokenIds[0] = tokenId4;
    changes[1].outputTokenIds = new uint256[](1);
    changes[1].outputTokenIds[0] = tokenId2;
    
    // Each change will take 0.0025 ETH
    uint256 value = 2* 0.0025 ether;
    
    // The following action will fail even though everything should be correct. The main issue is that the `change` funtion will try to forward `msg.value` to each call of the PrivatePool.change. The first call will take the fee (0.0025) and refund the excess (0.0025), but the second call will try to forward again 0.0050 ETH and will fail since the balance is only 0.0025 ETH.
    vm.expectRevert();
    ethRouter.change{value: value}(changes, 0);
    
    vm.stopPrank();
}
```

## Recommendation

The `change` function could send the available contract balance to each call to the `change` function of the Private Pool contract. The pool will send the remainder ETH back to the EthRouter, and the EthRouter can continue sending the available amount in the contract to subsequent calls to the `change` function. Any ETH left at the end of the function is already refunded to the caller. 

```solidity
PrivatePool(_change.pool).change{value: address(this).balance}(
    _change.inputTokenIds,
    _change.inputTokenWeights,
    _change.inputProof,
    _change.stolenNftProofs,
    _change.outputTokenIds,
    _change.outputTokenWeights,
    _change.outputProof
);
```
