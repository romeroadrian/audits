# Private Pool creation in Factory contract should check implementation is initialized

## Impact

Private pools in the Factory contract are created using minimal clones, which are thin proxies that delegate functionality to a given implementation address.

https://github.com/code-423n4/2023-04-caviar/blob/main/src/Factory.sol#L92

```solidity
privatePool = PrivatePool(payable(privatePoolImplementation.cloneDeterministic(_salt)));
```

The `privatePoolImplementation` variable represents the address of the implementation used when a new private pool is created. This address isn't configured during Factory construction time. This means that private pools which are created before the implementation address is defined can be created and even initialized successfully, but will lack any functionality as the proxy will point to the `address(0)`.

Given this scenario, if funds (ETH or ERC20 tokens) are sent to the pool as part of the initialization process, these will be lost as the implementation will be empty and have no code, and there won't be any way of recovering these funds.

## Proof of Concept

In the following test we demonstrate the issue by creating a Factory contract with an uninitialized pool implementation. Bob creates a private pool using the factory and sends some ETH to it. The call will succeed but the private pool will be created with an empty implementation, which will prevent any functionality on the pool. 

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/06238839330315780b90d9202042ea0f).

```solidity
function test_Factory_create_UninitializedImplementation() public {
    // Factory with uninitialized pool implementation
    Factory factory = new Factory();

    // Setup
    vm.deal(bob, 1e18);

    // Bob will create a pool and loose funds
    vm.startPrank(bob);

    uint256[] memory tokenIds = new uint256[](0);

    PrivatePool privatePool = factory.create{value: 1e18}(
        address(0), // address _baseToken
        address(milady), // address _nft
        100e18, // uint128 _virtualBaseTokenReserves
        10e18, // uint128 _virtualNftReserves
        200, // uint56 _changeFee
        100, // uint16 _feeRate
        bytes32(0), // bytes32 _merkleRoot
        false, // bool _useStolenNftOracle
        false, // bool _payRoyalties
        bytes32(0), // bytes32 _salt
        tokenIds, // uint256[] memory tokenIds
        1e18 // uint256 baseTokenAmount
    );

    // Funds are in the proxy but implementation is address(0)
    assertEq(address(privatePool).balance, 1e18);
    
    // Bob tries to withdraw funds but the call won't do anything as there is no implementation
    privatePool.withdraw(address(milady), tokenIds, address(0), 1e18);
    
    // Funds are still in the pool
    assertEq(address(privatePool).balance, 1e18);

    vm.stopPrank();
}
```

## Recommendation

The `create` function should first validate that the implementation address (`privatePoolImplementation`) is correctly initialized.

```solidity
  function create(
      address _baseToken,
      address _nft,
      uint128 _virtualBaseTokenReserves,
      uint128 _virtualNftReserves,
      uint56 _changeFee,
      uint16 _feeRate,
      bytes32 _merkleRoot,
      bool _useStolenNftOracle,
      bool _payRoyalties,
      bytes32 _salt,
      uint256[] memory tokenIds, // put in memory to avoid stack too deep error
      uint256 baseTokenAmount
  ) public payable returns (PrivatePool privatePool) {
      // check that the msg.value is equal to the base token amount if the base token is ETH or the msg.value is equal
      // to zero if the base token is not ETH
      if ((_baseToken == address(0) && msg.value != baseTokenAmount) || (_baseToken != address(0) && msg.value > 0)) {
          revert PrivatePool.InvalidEthAmount();
      }
      
+     require(privatePoolImplementation != address(0), "Uninitialized implementation");

      // deploy a minimal proxy clone of the private pool implementation
      privatePool = PrivatePool(payable(privatePoolImplementation.cloneDeterministic(_salt)));

      // mint the nft to the caller
      _safeMint(msg.sender, uint256(uint160(address(privatePool))));

      // initialize the pool
      privatePool.initialize(
          _baseToken,
          _nft,
          _virtualBaseTokenReserves,
          _virtualNftReserves,
          _changeFee,
          _feeRate,
          _merkleRoot,
          _useStolenNftOracle,
          _payRoyalties
      );

      if (_baseToken == address(0)) {
          // transfer eth into the pool if base token is ETH
          address(privatePool).safeTransferETH(baseTokenAmount);
      } else {
          // deposit the base tokens from the caller into the pool
          ERC20(_baseToken).transferFrom(msg.sender, address(privatePool), baseTokenAmount);
      }

      // deposit the nfts from the caller into the pool
      for (uint256 i = 0; i < tokenIds.length; i++) {
          ERC721(_nft).safeTransferFrom(msg.sender, address(privatePool), tokenIds[i]);
      }

      // emit create event
      emit Create(address(privatePool), tokenIds, baseTokenAmount);
  }
```
