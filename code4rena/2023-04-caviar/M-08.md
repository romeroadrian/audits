# Deposit function should be only accessible by the pool owner

## Impact

The `deposit` function present in the EthRouter contract can be used to deposit multiple NFTs and ETH funds into a Private Pool.

https://github.com/code-423n4/2023-04-caviar/blob/main/src/EthRouter.sol#L219-L248

```solidity
219:     function deposit(
220:         address payable privatePool,
221:         address nft,
222:         uint256[] calldata tokenIds,
223:         uint256 minPrice,
224:         uint256 maxPrice,
225:         uint256 deadline
226:     ) public payable {
227:         // check deadline has not passed (if any)
228:         if (block.timestamp > deadline && deadline != 0) {
229:             revert DeadlinePassed();
230:         }
231: 
232:         // check pool price is in between min and max
233:         uint256 price = PrivatePool(privatePool).price();
234:         if (price > maxPrice || price < minPrice) {
235:             revert PriceOutOfRange();
236:         }
237: 
238:         // transfer NFTs from caller
239:         for (uint256 i = 0; i < tokenIds.length; i++) {
240:             ERC721(nft).safeTransferFrom(msg.sender, address(this), tokenIds[i]);
241:         }
242: 
243:         // approve pair to transfer NFTs from router
244:         ERC721(nft).setApprovalForAll(privatePool, true);
245: 
246:         // execute deposit
247:         PrivatePool(privatePool).deposit{value: msg.value}(tokenIds, msg.value);
248:     }
```

This function is intended to be used by the owner of the pool, as these are private pools. However, the function lacks any access control and can be called by any account. 

A non-owner user can mistakenly deposit funds into a Private Pool thinking this action will yield some LP tokens and own a portion of the pool. Any assets deposited using this function will be part of the private pool, which is owned by the owner of the pool.

## Proof of Concept

In the following test, Alice setups a Private Pool while Bob mistakenly calls `deposit` in the EthRouter contract to deposit an NFT into the pool.

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/06238839330315780b90d9202042ea0f).

```solidity
function test_EthRouter_deposit_AnyoneCanDeposit() public {
    // Alice creates a pool
    uint256[] memory tokenIds;
    vm.prank(alice);
    PrivatePool privatePool = factory.create(
        address(0), // address _baseToken
        address(milady), // address _nft
        100e18, // uint128 _virtualBaseTokenReserves
        10e18, // uint128 _virtualNftReserves
        0, // uint56 _changeFee
        0, // uint16 _feeRate
        bytes32(0), // bytes32 _merkleRoot
        false, // bool _useStolenNftOracle
        false, // bool _payRoyalties
        bytes32(0), // bytes32 _salt
        tokenIds, // uint256[] memory tokenIds
        0 // uint256 baseTokenAmount
    );
    
    // However, Bob is allowed to deposit in this pool and will loose all funds
    vm.startPrank(bob);
    
    vm.deal(bob, 1 ether);
    uint256 tokenId = 0;
    milady.mint(bob, tokenId);
    
    milady.setApprovalForAll(address(ethRouter), true);
    
    tokenIds = new uint256[](1);
    tokenIds[0] = tokenId;
    
    ethRouter.deposit(
        payable(privatePool), // address payable privatePool,
        address(milady), // address nft,
        tokenIds, // uint256[] calldata tokenIds,
        0, // uint256 minPrice,
        type(uint256).max, // uint256 maxPrice,
        0 // uint256 deadline
    );
    
    vm.stopPrank();
}
```

## Recommendation

As this is a high level function part of the EthRouter periphery contract, the implementation should check that the caller is the owner of the pool (the holder of the NFT token that corresponds to the given Private Pool) to prevent any accidental calls that incorrectly use this function.
