# Token ids can differ from the delegatee address

## Summary

The entrypoint interface allows users to delegate to sources outside the address space, causing issues when the ERC1155 token id doesn't match the delegatee address.

## Impact

In ERC20MultiDelegate, delegations are represented by an ERC1155 token. When a user delegates voting power through the protocol, an ERC1155 token with an id matching the address of the delegatee is minted based on the number of votes being delegated.

Since ERC1155 token ids are `uint256`, the main entrypoint receives both sources and targets addresses as `uint256`, which are then casted to `address` internally.

https://github.com/code-423n4/2023-10-ens/blob/main/contracts/ERC20MultiDelegate.sol#L57-L61

```solidity
57:     function delegateMulti(
58:         uint256[] calldata sources,
59:         uint256[] calldata targets,
60:         uint256[] calldata amounts
61:     ) external {
```

https://github.com/code-423n4/2023-10-ens/blob/main/contracts/ERC20MultiDelegate.sol#L90-L95

```solidity
90:             address source = transferIndex < sourcesLength
91:                 ? address(uint160(sources[transferIndex]))
92:                 : address(0);
93:             address target = transferIndex < targetsLength
94:                 ? address(uint160(targets[transferIndex]))
95:                 : address(0);
```

The issue here is that the input types have a bigger space than the address type. It is possible to send a source argument with a value different from the intended address, but such that when downcasted (note the downcast is not a safe operation here) the address is valid.

This will have the effect of having an ERC1155 token with an id different from the address of the delegatee. Among its impact, and further elaborated in the proof of concept, this will cause issues while transferring the delegation. When delegations are moved between a source and a target, the ERC1155 balance check is done using the address, which is then upcasted to a `uint256`:

https://github.com/code-423n4/2023-10-ens/blob/main/contracts/ERC20MultiDelegate.sol#L124-L131

```solidity
124:     function _processDelegation(
125:         address source,
126:         address target,
127:         uint256 amount
128:     ) internal {
129:         uint256 balance = getBalanceForDelegate(source);
130: 
131:         assert(amount <= balance);
```

https://github.com/code-423n4/2023-10-ens/blob/main/contracts/ERC20MultiDelegate.sol#L192-L196

```solidity
192:     function getBalanceForDelegate(
193:         address delegate
194:     ) internal view returns (uint256) {
195:         return ERC1155(this).balanceOf(msg.sender, uint256(uint160(delegate)));
196:     }
```

Since the token id doesn't match the source address, the balance will be zero, and the call will revert in line 131.

## Proof of Concept

In the following test, the user delegates their voting power to the `delegate1` address but some upper bit is modified so that `target != delegate1` but `uint160(target) == delegate1`. As described in the previous section, the delegation will work but will cause an error when the user tries to transfer the delegation from `delegate1` to `delegate2`.

Note: the snippet shows only the relevant code for the test. Full test file can be found [here](https://gist.github.com/romeroadrian/8cdc62fd22e755406b51a70352b687c3).

```solidity
function test_MintBadTokenId() public {
    uint256[] memory sources;
    uint256[] memory targets;
    uint256[] memory amounts;

    uint256 amount = 1000e18;
    token.mint(user, amount);

    vm.startPrank(user);

    // approve to multi
    token.approve(address(multi), type(uint256).max);

    // User to delegates to delegate1 but uint256 also has 1 upper bit on
    uint256 delegateAddress = uint256(uint160(delegate1)) + (1 << 160);

    sources = new uint256[](0);
    targets = new uint256[](1);
    amounts = new uint256[](1);

    targets[0] = delegateAddress;
    amounts[0] = amount;

    multi.delegateMulti(sources, targets, amounts);

    // delegate1 successfully has the voting power
    assertEq(token.getVotes(delegate1), amount);

    // Now user wants to transfer the delegation to delegate2
    sources = new uint256[](1);
    targets = new uint256[](1);
    amounts = new uint256[](1);

    sources[0] = delegateAddress;
    targets[0] = uint256(uint160(delegate2));
    amounts[0] = amount;

    // The following fails because user doesn't have the tokenId that really represents delegate1
    vm.expectRevert();
    multi.delegateMulti(sources, targets, amounts);

    vm.stopPrank();
}
```

## Recommendation

Change the `delegateMulti()` interface to receive the proper `address` type instead of `uint256` for sources and targets. Instead of casting `uint256` to `address`, do the way around and cast the `address` to `uint256` to use as the ERC1155 token id.

This will not only fix the issue, but provide a better interface.

```diff
    function delegateMulti(
-       uint256[] calldata sources,
-       uint256[] calldata targets,
+       address[] calldata sources,
+       address[] calldata targets,
        uint256[] calldata amounts
    ) external {
        _delegateMulti(sources, targets, amounts);
    }
```
