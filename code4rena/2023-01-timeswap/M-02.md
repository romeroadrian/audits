# Incorrect token owner enumeration in `ERC1155Enumerable`

The `ERC1155Enumerable` contracts provides a base functionality to enumerate tokens owned by particular accounts. However, there are multiple flaws in the implementation.

The `_addTokenToOwnerEnumeration` function is called when the account receives a token and its current balance is 0, meaning this account now has a new token and should be added to the enumeration. Enumeration is confusingly provided by a mapping of account (address) -> index (uint256) -> token id (uint256), which is the internal variable `_ownedTokens` of type `mapping(address => mapping(uint256 => uint256))`. 

https://github.com/code-423n4/2023-01-timeswap/blob/main/packages/v2-token/src/base/ERC1155Enumerable.sol#L116-L121

```solidity
116 function _addTokenToOwnerEnumeration(address to, uint256 tokenId) private {
117     _currentIndex[to] += 1;
118     uint256 length = _currentIndex[to];
119     _ownedTokens[to][length] = tokenId;
120     _ownedTokensIndex[tokenId] = length;
121 }
```

In first place, `_currentIndex` (which is of type `mapping(address => uint256)`) is incremented before using it in line 117. This will cause the implementation to miss the zero index, and start from the second place (index 1). The index is read again in line 118 and stored in the enumeration in line 119.

Then, the logic presumably tries to keep a lookup table between token id -> index using the `_ownedTokensIndex` variable which is of type `mapping(uint256 => uint256)` . This is also wrong, since ERC1155 tokens can have multiple amounts and multiple owners, as opposed to an ERC721. 

If the token is transferred or burned, the logic to remove it from the owner enumeration happens in the `_removeTokenFromOwnerEnumeration` function.

https://github.com/code-423n4/2023-01-timeswap/blob/main/packages/v2-token/src/base/ERC1155Enumerable.sol#L136-L149

```solidity
136 function _removeTokenFromOwnerEnumeration(address from, uint256 tokenId) private {
137     uint256 lastTokenIndex = _currentIndex[from] - 1;
138     uint256 tokenIndex = _ownedTokensIndex[tokenId];
139 
140     if (tokenIndex != lastTokenIndex) {
141         uint256 lastTokenId = _ownedTokens[from][lastTokenIndex];
142 
143         _ownedTokens[from][tokenIndex] = lastTokenId;
144         _ownedTokensIndex[lastTokenId] = tokenIndex;
145     }
146 
147     delete _ownedTokensIndex[tokenId];
148     delete _ownedTokens[from][lastTokenIndex];
149 }
```

Line 137 tries to calculate the index of the last token by taking the current index and subtracting 1, which would be ok but it's wrong due to how `_addTokenToOwnerEnumeration` first increments the current index. Going back to the example, after a token is added to the enumeration `_currentIndex[from] == 1` meaning `lastTokenIndex == 0` which is incorrect because the token was stored in position 1.

Then, in line 138 it fetches the token index using the `_ownedTokensIndex` lookup table. This will be wrong if multiple accounts are minted the same token id, and will end up removing an index that potentially doesn't match the given token id. For example, let's say Alice gets minted a token and it is stored in `index == 2`, then `_ownedTokensIndex[tokenId] == 2`. But then Bob gets minted the same token id and is stored in `index == 1`, which will overwrite `_ownedTokensIndex[tokenId] == 1`. When Alice transfers or burns her token, `_ownedTokensIndex[tokenId]` will be 1, and this cause to remove the wrong token from Alice, since it will remove the one in index 1 and not the proper one which is in index 2. See PoC for a detailed walkthrough with code.

Notice also that `_removeTokenFromOwnerEnumeration` fails to update the `_currentIndex` variable for the account, which should be somehow decremented.

## PoC

The following test illustrates the scenarios detailed in the previous section.

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity =0.8.8;

import "forge-std/Test.sol";
import "../src/base/ERC1155Enumerable.sol";

contract TestERC1155Enumerable is ERC1155Enumerable {
    constructor() ERC1155("") {
    }

    function mint(address to, uint256 id, uint256 amount) external {
        _mint(to, id, amount, "");
    }

    function burn(address from, uint256 id, uint256 amount) external {
        _burn(from, id, amount);
    }
}

contract AuditTest is Test {
  function test_ERC1155Enumerable_WrongOwnerEnumeration() public {
        TestERC1155Enumerable token = new TestERC1155Enumerable();
        address alice = makeAddr("alice");
        address bob = makeAddr("bob");

        uint256 tokenId1 = 0x123;
        uint256 tokenId2 = 0x456;

        token.mint(alice, tokenId1, 1);

        // The first index (0) is skipped and returns 0
        // The token has been added to the second position (index == 1)
        assertEq(token.tokenOfOwnerByIndex(alice, 0), 0);
        assertEq(token.tokenOfOwnerByIndex(alice, 1), tokenId1);

        token.mint(alice, tokenId2, 1);
        token.mint(bob, tokenId2, 1);

        // Now Alice has tokenId2 in third position (index == 2)
        assertEq(token.tokenOfOwnerByIndex(alice, 2), tokenId2);

        // We burn tokenId2 from Alice
        token.burn(alice, tokenId2, 1);

        // But this removes tokenId1 from the enumeration (the one in index == 1) not tokenId2!
        // This happens because when we minted Bob's token it overwrite the _ownedTokensIndex
        assertEq(token.tokenOfOwnerByIndex(alice, 0), 0);
        assertEq(token.tokenOfOwnerByIndex(alice, 1), 0);
        assertEq(token.tokenOfOwnerByIndex(alice, 2), tokenId2);
    }
}
```

## Recommendation

Apply a similar strategy and data structures as the all tokens enumeration.

Instead of keeping a `mapping(address => mapping(uint256 => uint256))` for the `_ownedTokens` variable, use an array scoped to the account. For example `mapping(address => uint256[])`, given an account (address) returns all the token ids owned by that account (array of uint256).

The lookup table `_ownedTokensIndex` should also be scoped to the user, by using a `mapping(address => mapping(uint256 => uint256))`. Given an account (address) and a token id (uint256), it should return the index (uint256) in the `_ownedTokens` array.


