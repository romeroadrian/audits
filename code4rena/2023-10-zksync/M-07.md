# L2 Bridge contracts may fail to be deployed

## Summary

When deployed, bridge contracts in Ethereum execute a deployment of their counterpart in L2. These are L1->L2 transactions that may fail to be executed in L2, resulting in an inconsistent state between L1 and L2 contracts.

## Impact

Both WETH and ERC20 bridge contracts are deployed by creating a contract in Ethereum (L1) that requests a deployment of their counterparts in L2 as part of their initialization process. Taking the L1WethBridge as the example, we can see its `initialize` function requests a deployment of the proxy and the implementation contract of  the L2WethBridge contract.

https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/contracts/ethereum/contracts/bridge/L1WethBridge.sol#L98-L134

```solidity
098:         bytes32 l2WethBridgeImplementationBytecodeHash = L2ContractHelper.hashL2Bytecode(_factoryDeps[0]);
099:         bytes32 l2WethBridgeProxyBytecodeHash = L2ContractHelper.hashL2Bytecode(_factoryDeps[1]);
100: 
101:         // Deploy L2 bridge implementation contract
102:         address wethBridgeImplementationAddr = BridgeInitializationHelper.requestDeployTransaction(
103:             zkSync,
104:             _deployBridgeImplementationFee,
105:             l2WethBridgeImplementationBytecodeHash,
106:             "", // Empty constructor data
107:             _factoryDeps // All factory deps are needed for L2 bridge
108:         );
109: 
110:         // Prepare the proxy constructor data
111:         bytes memory l2WethBridgeProxyConstructorData;
112:         {
113:             // Data to be used in delegate call to initialize the proxy
114:             bytes memory proxyInitializationParams = abi.encodeCall(
115:                 IL2WethBridge.initialize,
116:                 (address(this), l1WethAddress, _l2WethAddress)
117:             );
118:             l2WethBridgeProxyConstructorData = abi.encode(
119:                 wethBridgeImplementationAddr,
120:                 _governor,
121:                 proxyInitializationParams
122:             );
123:         }
124: 
125:         // Deploy L2 bridge proxy contract
126:         l2Bridge = BridgeInitializationHelper.requestDeployTransaction(
127:             zkSync,
128:             _deployBridgeProxyFee,
129:             l2WethBridgeProxyBytecodeHash,
130:             l2WethBridgeProxyConstructorData,
131:             // No factory deps are needed for L2 bridge proxy, because it is already passed in the previous step
132:             new bytes[](0)
133:         );
134:     }
```

The implementation uses the helper library BridgeInitializationHelper to request the deployment in L2:

https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/contracts/ethereum/contracts/bridge/libraries/BridgeInitializationHelper.sol#L30-L58

```solidity
30:     function requestDeployTransaction(
31:         IZkSync _zkSync,
32:         uint256 _deployTransactionFee,
33:         bytes32 _bytecodeHash,
34:         bytes memory _constructorData,
35:         bytes[] memory _factoryDeps
36:     ) internal returns (address deployedAddress) {
37:         bytes memory deployCalldata = abi.encodeCall(
38:             IL2ContractDeployer.create2,
39:             (bytes32(0), _bytecodeHash, _constructorData)
40:         );
41:         _zkSync.requestL2Transaction{value: _deployTransactionFee}(
42:             L2_DEPLOYER_SYSTEM_CONTRACT_ADDR,
43:             0,
44:             deployCalldata,
45:             DEPLOY_L2_BRIDGE_COUNTERPART_GAS_LIMIT,
46:             REQUIRED_L2_GAS_PRICE_PER_PUBDATA,
47:             _factoryDeps,
48:             msg.sender
49:         );
50: 
51:         deployedAddress = L2ContractHelper.computeCreate2Address(
52:             // Apply the alias to the address of the bridge contract, to get the `msg.sender` in L2.
53:             AddressAliasHelper.applyL1ToL2Alias(address(this)),
54:             bytes32(0), // Zero salt
55:             _bytecodeHash,
56:             keccak256(_constructorData)
57:         );
58:     }
```

There are two important details here. One is that the address of the contract in L2 can be precomputed since the deployment will be done deterministically using CREATE2, this allows the L1 bridge contract to store the address of its counterpart in L2 without depending on any result of the actual deployment. Second, the deployment of the contract in L2 is handled by a L1->L2 transaction to the deployer contract in L2 using `requestL2Transaction()`.

As stated in [the documentation](https://github.com/code-423n4/2023-10-zksync/blob/main/docs/Smart%20contract%20Section/Handling%20L1%E2%86%92L2%20ops%20on%20zkSync.md), L1->L2 transactions may fail. While the protocol validates that L1->L2 transactions can be proved and reported back to L1, there is no guarantee that the actual transaction will succeed. The transaction in L2 can successfully be executed with a failed result.

In this scenario, the bridge will have an inconsistent state with its contracts in L1 deployed but nothing on the L2 side. It is also important to note that L2 deployment is triggered from the `initializer` function, which can run just once. There is no mechanism for trying to redeploy those contracts from the already initialized L1 contracts.

Additionally, it is important to note that L1 bridges are operative as soon as they are deployed. Users can deposit funds, which will trigger another L1->L2 to an empty contract. Taking again the L1WethBridge as the example (code is similar in L1ERC20Bridge), we can see the `deposit()` function sends a messages to the `l2Bridge` address:

https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/contracts/ethereum/contracts/bridge/L1WethBridge.sol#L185-L193

```solidity
185:         txHash = zkSync.requestL2Transaction{value: _amount + msg.value}(
186:             l2Bridge,
187:             _amount,
188:             l2TxCalldata,
189:             _l2TxGasLimit,
190:             _l2TxGasPerPubdataByte,
191:             new bytes[](0),
192:             refundRecipient
193:         );
```

These transactions will ultimately try to call to an account with empty bytecode on the L2 side. This means that L2 transactions that derive from `deposit()` calls in L1 can potentially be executed successfully, leading to loss of funds. Following the trail of L2 execution we have:

- [`bootloader::processL1Tx()`](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/bootloader/bootloader.yul#L863)
- [`bootloader::getExecuteL1TxAndGetRefund()`](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/bootloader/bootloader.yul#L977)
- [`bootloader::ZKSYNC_NEAR_CALL_executeL1Tx()`](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/bootloader/bootloader.yul#L1632)
- [`bootloader::executeL1Tx()`](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/bootloader/bootloader.yul#L2318)
- [`bootloader::msgValueSimulatorMimicCall()`](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/bootloader/bootloader.yul#L1763)
- [`MsgValueSimulator::fallback()`](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/contracts/MsgValueSimulator.sol#L35)
- [`EfficientCall::mimicCall()`](https://github.com/code-423n4/2023-10-zksync/blob/1fb4649b612fac7b4ee613df6f6b7d921ddd6b0d/code/system-contracts/contracts/libraries/EfficientCall.sol#L105)

This results in a `mimiccall`, which is a raw low level call that mimics the original sender from L1. As this is a low level call, its execution will still succeed, since the `l2Bridge` account has empty code. The transaction in L2 will be successful, and funds will be locked in L1 without having a way to redeem them in L2, or send them back.

## Proof of Concept

Following the described scenario in the previous section, the issue for the L1WethBridge can be summarized via the following steps (L1ERC20Bridge is similar):

1. The L1WethBridge contract is deployed in L2, which triggers the deployment of the contract in L2.
2. The L2 transaction to deploy the contracts in L2 fails. 
3. A user calls `deposit()` in L1WethBridge and sends an amount of ETH to be bridged. This fires a L1->L2 transaction to call `IL2Bridge.finalizeDeposit()` to the `l2Bridge` account.
4. The L2 transaction is processed in L2, this executes a call to `l2Bridge` which has empty bytecode. The transaction succeeds and the ETH amount (now in L2) is sent to this account.
5. The funds are now lost in L2, the user has no way of redeeming them on the L2 side. On the L1 side, funds are locked in the bridge and cannot be recovered, since the L2 bridge is missing and cannot send them back.

## Recommendation

A potential solution would be to enable the bridges as soon as the L1->L2 transactions are proved to be successfully executed in L2, which can be done using `Mailbox::proveL1ToL2TransactionStatus()`.

1. Start the bridge in a paused state.
2. When requesting the deployment of the L2 bridges, store the transaction hashes returned by `requestL2Transaction()`.
3. Add a function to try to enable the bridge. This will receive the L2 batch number and index and use `Mailbox::proveL1ToL2TransactionStatus()` to prove the transaction with the corresponding transaction hash has been ex. If this is successful, enable the bridge.

The solution could also include a way to re-request the deployment of the L2 bridges in case the transaction in L2 fails.
