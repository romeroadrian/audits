# DOS of bridges when ETH deposit limit is set on the AllowList

## Summary

The `AllowList` establishes token limits for deposits. 

It is expected that an ETH deposit limit can be set at any time, as the [Mailbox verifies this ETH deposit limit](https://github.com/code-423n4/2023-10-zksync/blob/main/code/contracts/ethereum/contracts/zksync/facets/Mailbox.sol#L276-L279) whenever an L1->L2 transaction is requested. 

The limit is set per `address` that requests an L1->L2 transaction and is designed to impose limits on external actors.

The problem is that zkSync protocol bridges have the same limits as any other external user, and will quickly reach the maximum value per normal usage, or intentionally by an adversary trying to exploit this. ETH is used on L1->L2 txs by the bridges to pay fees, but it is most notable in the case of the `L1WethBridge`, where ETH is also converted from WETH to send to L2.

## Impact

Affected bridges will be in a DOS state where no bridge deposit from L1 to L2 will be possible for any user.

The only quick mitigation by the protocol would be to remove the ETH deposit limit (nullifying its functionality), until an upgrade is done to actually fix the issue.

In the meantime the bridge functionality to deposit assets will be out of service.

Assessing the severity as Medium considering "function of the protocol or its availability could be impacted". The likability of the issue is expected at any time [since the code already accounts for this limit](https://github.com/code-423n4/2023-10-zksync/blob/main/code/contracts/ethereum/contracts/zksync/facets/Mailbox.sol#L276-L279).

## Proof of Concept

zkSync bridges deposit assets to L2 via an L1->L2 transaction.

In the case of the `L1WethBridge`, for example, it first [converts WETH to ETH](https://github.com/code-423n4/2023-10-zksync/blob/main/code/contracts/ethereum/contracts/bridge/L1WethBridge.sol#L170-L173) and then requests an L1->L2 transaction via the `Mailbox` with the `msg.value` being [the sum of the converted ETH + fees](https://github.com/code-423n4/2023-10-zksync/blob/main/code/contracts/ethereum/contracts/bridge/L1WethBridge.sol#L185).

The `Mailbox` then checks for ETH limits and reverts the transaction [if the depositor reaches the limit](https://github.com/code-423n4/2023-10-zksync/blob/main/code/contracts/ethereum/contracts/zksync/facets/Mailbox.sol#L279).

The `Mailbox` considers this `depositor` to be the [`msg.sender` of the `requestL2Transaction()` call](https://github.com/code-423n4/2023-10-zksync/blob/main/code/contracts/ethereum/contracts/zksync/facets/Mailbox.sol#L261), which in the case of bridge deposits, are the bridges themselves.

This means that the WETH bridge whose main function is to convert WETH to ETH and bridge it to L2 for all users, will have the same ETH limit as any normal user that sends ETH directly via the `Mailbox` to pay fees for L1->L2 transactions.

Once the limit is reached by the bridge, the deposit functionality will enter in a DOS state

### Coded Proof of Concept

Add this test to `code/contracts/ethereum/test/foundry/unit/concrete/Bridge/L1WethBridge/Deposit.t.sol` and run `forge test --mt "test_DepositDOS"` to verify how bridges are affected by deposits from different users, and enter in a DOS state for everyone:

```solidity
  function test_DepositDOS() public {
      // Set the deposit limit to 10 ETH
      // This will affect all addresses, including the bridge!
      vm.prank(owner);
      allowList.setDepositLimit(address(0), true, 10 ether);

      uint256 fees = 0.01 ether;

      // The first depositor deposits 9 ETH and the tx succeeds
      address depositor1 = makeAddr("depositor1");
      deal(depositor1, 10 ether);
      vm.startPrank(depositor1);

      uint256 amount1 = 9 ether;
      l1Weth.deposit{value: amount1}();
      l1Weth.approve(address(bridgeProxy), amount1);

      bytes memory depositCallData1 = abi.encodeWithSelector(
          bridgeProxy.deposit.selector,
          depositor1,
          bridgeProxy.l1WethAddress(),
          amount1,
          1000000,
          REQUIRED_L2_GAS_PRICE_PER_PUBDATA,
          depositor1
      );

      (bool success1, ) = address(bridgeProxy).call{value: fees}(depositCallData1);
      assertEq(success1, true);

      // The second depositor tries to deposit only 2 ETH and the tx reverts
      // The bridge is now in a DOS state, not allowing any additional deposits
      address depositor2 = makeAddr("depositor2");
      deal(depositor2, 10 ether);
      changePrank(depositor2);

      uint256 amount2 = 2 ether;
      l1Weth.deposit{value: amount2}();
      l1Weth.approve(address(bridgeProxy), amount2);

      bytes memory depositCallData2 = abi.encodeWithSelector(
          bridgeProxy.deposit.selector,
          depositor2,
          bridgeProxy.l1WethAddress(),
          amount2,
          1000000,
          REQUIRED_L2_GAS_PRICE_PER_PUBDATA,
          depositor2
      );

      (bool success2, ) = address(bridgeProxy).call{value: fees}(depositCallData2);
      assertEq(success2, false);
  }
```

## Recommendation

Depending on protocol needs, limits for bridges may be removed completely, or special limits may be applied.

Here's a suggested implementation if the decision is to completely remove ETH limits for bridges:

```diff
  function _verifyDepositLimit(address _depositor, uint256 _amount) internal {
+     if (msg.sender == address(wethBridge) || msg.sender == address(erc20Bridge)) return;

      IAllowList.Deposit memory limitData = IAllowList(s.allowList).getTokenDepositLimitData(address(0)); // address(0) denotes the ETH
      if (!limitData.depositLimitation) return; // no deposit limitation is placed for ETH

      require(s.totalDepositedAmountPerUser[_depositor] + _amount <= limitData.depositCap, "d2");
      s.totalDepositedAmountPerUser[_depositor] += _amount;
  }
```
