# Malicious user can frontrun the selling or transferring of a ticket to claim the rewards

A bad actor can trick a user into buying an unclaimed ticket and frontrun the selling or transfer of the NFT to claim the rewards associated with the ticket before the original transaction.

## Impact

Given the described scenario, a bad actor can frontrun the transaction associated with the exchange of an unclaimed ticket with a transaction to claim the rewards associated with the ticket in the lottery. The malicious user will end up with both the ticket winnings and the sale revenue.

## Proof of concept

Here "bad actor" is selling the unclaimed ticket NFT and "buyer" wishes to purchase it.

1. Bad actor lists Ticket NFT in secondary market .
2. Buyer sends transaction to purchase/exchange NFT.
3. Bad actor frontruns previous transaction to claim winnings from ticket.
4. Transaction from (2) succeeds and the purchase is executed but with an already claimed ticket.

## Recommendation

- When a ticket is claimed, burn it or make it untransferable, or
- When a ticket is claimed, add a cooldown period and prevent any transfer during this period.

Quick example:

```solidity
abstract contract Ticket is ITicket, ERC721 {
    uint256 constant TRANSFER_COOLDOWN = 1 hours;

    uint256 public override nextTicketId;
    mapping(uint256 => ITicket.TicketInfo) public override ticketsInfo;
    mapping(uint256 => uint256) private claimedAt;

    // solhint-disable-next-line no-empty-blocks
    constructor() ERC721("Wenwin Lottery Ticket", "WLT") { }

    function markAsClaimed(uint256 ticketId) internal {
        ticketsInfo[ticketId].claimed = true;
        claimedAt[ticketId] = block.timestamp;
    }

    function mint(address to, uint128 drawId, uint120 combination) internal returns (uint256 ticketId) {
        ticketId = nextTicketId++;
        ticketsInfo[ticketId] = TicketInfo(drawId, combination, false);
        _mint(to, ticketId);
    }

    function _beforeTokenTransfer(
        address from,
        address to,
        uint256 tokenId,
        uint256 batchSize
    ) internal override {
        super._beforeTokenTransfer(from, to, tokenId, batchSize);
        require(block.timestamp >= claimedAt[tokenId] + TRANSFER_COOLDOWN);
    }
}
```
